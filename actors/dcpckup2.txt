//-------------------------------------------------
// Ammo pickups
//-------------------------------------------------

ACTOR ClipMagPickup : RandomSpawner replaces Clip
{
	dropitem ZM66RifleMags 256 12
	dropitem HDPistolMag 256 2
	dropitem HDSMGMag 256 1
	dropitem ArmorBonus 256 1
}
ACTOR ClipBoxPickup : RandomSpawner replaces ClipBox
{
	dropitem ClipBoxPickup1 256 7
	dropitem ClipBoxPickup2 256 1
	dropitem HDAB 256 2
}
ACTOR ClipBoxPickup1 : HDPickup
{
	inventory.pickupmessage "Picked up a 4.26 UAC Standard magazine."
	xscale 0.3
	yscale 0.4
	states
	{
	spawn:
		TNT1 A 0
		CLIP A 0 A_Jump(176, 2)
		CLIP A 0 A_SpawnItemEx ("HDFragGrenadeAmmo", random(-12,12), random(-12,12), random(-4,4))
		CLIP A 0 A_Jump(186, 2)
		CLIP A 0 A_SpawnItemEx("HDRocketAmmo", random(-8,8), random(-8,8), random(-4,4))
		CLIP C 0 A_SpawnItemEx("ZM66Random", random(-4,4),random(-4,4),0)
		stop
	pickup:
		TNT1 A 0 A_GiveInventory("ZM66RifleMags",1)
		stop
	}
}
ACTOR ClipBoxPickup2 : HDPickup
{
	xscale 1
	yscale 1
	inventory.pickupmessage "Picked up a 30-round 9mm magazine."
	states
	{
	spawn:
		TNT1 A 0
		CLIP A 0 A_Jump (176, 2)
		CLIP A 0 A_SpawnItemEx ("HDFragGrenadeAmmo", random(-12,12), random(-12,12), random(-4,4))
		CLIP A 0 A_Jump (186, 2)
		CLIP A 0 A_SpawnItemEx ("HDSMGMag", random(-8,8), random(-8,8), random(-4,4))
		CLP3 A 0 A_SpawnItemEx ("HDSMG", random(-4,4),random(-4,4),0)
		stop
	pickup:
		TNT1 A 0 A_GiveInventory("HDSMGMag",1)
		stop
	}
}
actor ShellRandom : RandomSpawner replaces Shell
{
	dropitem ShellPickup 256 7
	dropitem HDFumbledShell 256 2
	dropitem DecoPusher 200 3
	dropitem BFGVileShard 200 1
	dropitem HDCellpacks 256 1
	dropitem ZM66RifleMags 256 1
	dropitem HDAB 196 1
}
actor SingleShellPickup : HDShellAmmo
{
	inventory.pickupmessage "Picked up a shotgun shell."
	inventory.amount 1
	states
	{
	spawn:
		TNT1 A 2
		TNT1 A 0 A_SpawnItemEx ("HDFumbledShell",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
		stop
	}
}
actor ShellBoxRandom : RandomSpawner replaces ShellBox
{
	dropitem ShellBoxPickup 256 10
	dropitem DecoPusher 200 1
	dropitem HDCellpacks 256 2
	dropitem HDAB 256 1
}
actor RocketBoxPickup : RandomSpawner replaces RocketBox
{
	dropitem RocketBigPickup 256 14
	dropitem HDFragGrenadePickup 256 3
}
Actor CellReplacer : RandomSpawner replaces Cell
{
	+ismonster
	dropitem BFGVileShard 128 1
	dropitem LiberatorMag 256 3
	dropitem BrontornisRound 256 2
	dropitem HDCellpacks 256 7
}
Actor CellPackReplacer : RandomSpawner replaces CellPack
{
	+ismonster
	dropitem BFGVileShard 196 1
	dropitem LiberatorMag 256 3
	dropitem BrontornisSpawner 256 1
	dropitem HDCellpacks 256 4
	dropitem HDAB 256 1
}




//-------------------------------------------------
//  The box of MYSTERY!
//-------------------------------------------------

Actor HDAB : RandomSpawner
{
	dropitem HDAmBox 256 1
	dropitem HDAmBoxUnarmed 256 5
}
actor TrapShooter : IdleDummy
{
	obituary "%o paid no attention to Admiral Ackbar."
	+lookallaround
	states
	{
	spawn:
		TNT1 A 0
		TNT1 A 0 A_Stop
		TNT1 AAAAAAAAAA 0 A_Look
		TNT1 A 1
		stop
	see:
		TNT1 A 0 A_FaceTarget(0,0)
		TNT1 A 0 A_SetPitch(pitch+frandom(-2,2))
		TNT1 A 0 A_SetAngle(angle+frandom(-2,2))
		TNT1 A 0 A_PlaySound("weapons/pistol")
		TNT1 A 0 A_CustomMissile("HDBullet9")
		TNT1 A 1 A_CustomMissile("HDSpent9mm")
		stop
	}
}
actor HDAmBox:HDUPK{
	scale 0.6
	height 16 radius 16
	+usespecial activation THINGSPEC_Switch|THINGSPEC_ThingTargets
	health 100 mass 120 +shootable +noblood +nopain radiusdamagefactor 0.5
	+lookallaround +nofear +ghost
	meleerange 42
	states
	{
	spawn:
		AMMO A -1
		stop
	tap:
		AMMO A 0 A_JumpIfInTargetInventory("IsMoving",1,2)
		AMMO A 0 A_Jump(256,2)
		AMMO A 20 A_ClearTarget
		goto spawn
		AMMO A 0 A_GiveInventory("Clip")
		AMMO A 0 A_JumpIfInventory("NotShot",random(5,10),"disarm")
		AMMO A 0 A_ChangeVelocity(0,0,1)
		AMMO A 10 A_GiveInventory("NotShot",1)
		AMMO A 0 A_TakeInventory("Clip")
		goto spawn
	grab:
		AMMO A 0 A_JumpIfInventory("Clip",1,1)
		goto tap
		AMMO A 0 A_JumpIfCloser(42,"open")
		goto spawn
	open:
		TNT1 A 0 A_Jump(4,"disarm")
		TNT1 A 0 A_Jump(4,"trapped")
		TNT1 A 0 A_JumpIfInventory("RedSkull",1,"disarm")
		TNT1 A 0 A_JumpIfInventory("BlueSkull",1,"disarm")
		TNT1 A 0 A_JumpIfInventory("YellowSkull",1,"disarm")
	death:
	trapped:
		TNT1 A 0 A_ChangeFlag("usespecial",0)
		TNT1 A 0 A_PlaySound("weapons/plasmax")
		TNT1 A 0 A_PlaySound("weapons/rifleclick",7)
		TNT1 A 0 A_Jump(250,1,3,4)
		goto disarm
		TNT1 A 0 A_SpawnItemEx("HDFragGrenade",0,0,10,1,0,3,0,SXF_NOCHECKPOSITION)
		TNT1 A 0 A_SpawnItemEx("HDFragSpoon",0,0,10,random(3,6),0,random(8,16),random(-12,12),SXF_NOCHECKPOSITION)
		goto brokengoodies
		TNT1 A 0 A_DropItem("BFGVileShard")
		goto brokengoodies
		TNT1 A 0 A_DropItem("TrapShooter")
		goto brokengoodies
	disarm:
		TNT1 A 0 A_Jump(250,1,2,3)
		goto trapped
		TNT1 A 0 A_DropItem("HDFragGrenadeAmmo") goto disarm2
		TNT1 A 0 A_DropItem("BFGVileShard") goto disarm2
		TNT1 A 0 A_DropItem("HDLoose9mm") goto disarm2
	disarm2:
		TNT1 A 0 A_SpawnItemEx("HDAmBoxDisarmed",0,0,1,0,0,0,0,SXF_NOCHECKPOSITION)
		stop
	goodies:
		TNT1 A 0 A_PlaySound("weapons/pocket")
		TNT1 A 0 A_Jump(256,"zeds","smgs","piss","libs","clip","rocs","bron","cell","shel","ieds")
		zeds: TNT1 AAA 0 A_DropItem("ZM66RifleMags") stop
		smgs: TNT1 AAA 0 A_DropItem("HDSMGMag") stop
		piss: TNT1 AAAAA 0 A_DropItem("HDPistolMag") stop
		libs: TNT1 AAA 0 A_DropItem("LiberatorMag") stop
		clip: TNT1 AAAAA 0 A_DropItem("BossClip") stop
		rocs: TNT1 AAAA 0 A_DropItem("HDRocketAmmo") stop
		bron: TNT1 AAA 0 A_DropItem("BrontornisRound") stop
		cell: TNT1 AAA 0 A_DropItem("HDCellPacks") stop
		shel: TNT1 AAAA 0 A_DropItem("ShellPickup") stop
		ieds: TNT1 AAAA 0 A_DropItem("HDIEDKits") stop
	brokengoodies:
		TNT1 AAA 0 A_SpawnItemEx("HDSmoke",random(-1,1),random(-1,1),random(-1,1),0,0,random(0,2),0,SXF_NOCHECKPOSITION)
		TNT1 A 0 A_Jump(256,"brokenzeds","brokensmgs","brokenpiss","brokenlibs","brokenclip","brokenrocs","brokenbron","brokencell","brokenshel")
	brokenzeds:
		TNT1 A 0 A_DropItem("ZM66RifleMags")
		TNT1 AA 0 A_DropItem("HD4mmMagEmpty")
		TNT1 A 0 A_SpawnItemEx("HDSmokeChunk",random(-10,10),random(-10,10),random(1,10),random(-4,4),random(-4,4),random(0,2),0,SXF_NOCHECKPOSITION,24)
		stop
	brokensmgs:
		TNT1 A 0 A_DropItem("HDSMGMag")
		TNT1 AA 0 A_DropItem("HDSMGEmptyMag")
		TNT1 AAAAAAAAAAAAAAA 0 A_SpawnItemEx("HDSpent9mm",random(-4,4),random(-4,4),5,0,random(1,360),random(-4,4),0,SXF_NOCHECKPOSITION,12)
		TNT1 AAAAAAAA 0 A_SpawnItemEx("HDLoose9mm",random(-4,4),random(-4,4),5,0,0,random(-4,4),random(1,360),SXF_NOCHECKPOSITION,24)
		stop
	brokenpiss:
		TNT1 A 0 A_DropItem("HDPistolMag")
		TNT1 AAAA 0 A_DropItem("HDPistolEmptyMag")
		TNT1 AAAAAAAAAAAAAAA 0 A_SpawnItemEx("HDSpent9mm",random(-4,4),random(-4,4),5,0,0,random(-4,4),random(1,360),SXF_NOCHECKPOSITION,24)
		TNT1 AAAAAAAA 0 A_SpawnItemEx("HDLoose9mm",random(-4,4),random(-4,4),5,0,0,random(-4,4),random(1,360),SXF_NOCHECKPOSITION,48)
		stop
	brokenlibs:
		TNT1 A 0 A_DropItem("LiberatorMag")
		TNT1 A 0 A_DropItem("LiberatorEmptyMag")
		TNT1 AAAAAAAAAA 0 A_SpawnItemEx("HDSpent7mm",random(-4,4),random(-4,4),5,0,0,random(-4,4),random(1,360),SXF_NOCHECKPOSITION,12)
		TNT1 AAAAAAAAAA 0 A_SpawnItemEx("HDLoose7mm",random(-4,4),random(-4,4),5,0,0,random(-4,4),random(1,360),SXF_NOCHECKPOSITION,36)
		stop
	brokenclip:
		TNT1 A 0 A_DropItem("BossClip")
		TNT1 AAAAAAAAAAAAAAAAAAAAAAAAA 0 A_SpawnItemEx("HDLoose7mm",random(-4,4),random(-4,4),5,0,0,random(-4,4),random(1,360),SXF_NOCHECKPOSITION)
		TNT1 AAAAAAAAAAAAAAA 0 A_SpawnItemEx("HDSpent7mm",random(-4,4),random(-4,4),5,0,0,random(-4,4),random(1,360),SXF_NOCHECKPOSITION)
		stop
	brokenrocs:
		TNT1 A 0 A_DropItem("HDRocketAmmo")
		TNT1 AA 0 A_DropItem("DudRocket")
		stop
	brokenbron:
		TNT1 A 0 A_DropItem("BrontornisRound")
		TNT1 AAA 0 A_SpawnItemEx("HDSmoke",random(-10,10),random(-10,10),random(1,10),0,0,random(0,2),0,SXF_NOCHECKPOSITION)
		TNT1 AAA 0 A_SpawnItemEx("HDGunSmoke",random(-1,1),random(-1,1),random(2,10),0,0,random(0,2),0,SXF_NOCHECKPOSITION)
		TNT1 A 0 A_SpawnItemEx("HDExplosion",random(-10,10),random(-10,10),random(1,10),0,0,random(0,2),0,SXF_NOCHECKPOSITION)
		TNT1 AAAA 0 A_SpawnItemEx("HDSmokeChunk",random(-10,10),random(-10,10),random(1,10),random(-6,6),random(-6,6),random(0,4),0,SXF_NOCHECKPOSITION,24)
		stop
	brokencell:
		TNT1 A 0 A_DropItem("HDCellPackEmpty")
		TNT1 A 0 A_DropItem("HDCellPacks")
		TNT1 AAA 0 A_SpawnItemEx("HDSmoke",random(-1,1),random(-1,1),random(1,4),0,0,random(0,2),0,SXF_NOCHECKPOSITION)
		TNT1 AAA 0 A_SpawnItemEx("HDGunSmoke",random(-1,1),random(-1,1),random(1,10),0,0,random(0,2),0,SXF_NOCHECKPOSITION)
		TNT1 A 0 A_SpawnItemEx("HDExplosion",random(-1,1),random(-1,1),random(3,4),0,0,random(0,2),0,SXF_NOCHECKPOSITION)
		TNT1 AAAAAA random(1,8) A_GiveInventory("CacoZapper")
		stop
	brokenshel:
		TNT1 A 0 A_DropItem("ShellPickup")
		TNT1 AAAAAAAAAAA 0 A_SpawnItemEx("HDFumblingShell",random(-4,4),random(-4,4),5,0,0,random(-4,4),random(1,360),SXF_NOCHECKPOSITION)
		TNT1 AAAAA 0 A_SpawnItemEx("HDSpentShell",random(-4,4),random(-4,4),5,0,0,random(-4,4),random(1,360),SXF_NOCHECKPOSITION)
		stop
	}
}
actor HDAmBoxUnarmed : HDAmBox
{
	states
	{
	open:
		"----" A 0 A_Jump(256,"goodies")
	death:
	trapped:
		"----" A 0 A_PlaySound("weapons/plasmax")
		"----" A 0 A_PlaySound("weapons/rifleclick",7)
		goto brokengoodies
	}
}
actor HDAmBoxDisarmed:HDAmBoxUnarmed{
	states{
	grab:
		"----" A 0 A_Jump(256,"goodies")
	spawn:
		OWWV A -1
		stop
	}
}



//-------------------------------------------------
// Health/armour bonuses
//-------------------------------------------------
actor HDFragGrenadePickup:FragP{
	states{
	spawn:
		FRAG A 0 nodelay{
			A_SpawnItemEx("FragP",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION);
			A_SpawnItemEx("FragP",-4,0,0,0,0,0,0,SXF_NOCHECKPOSITION);
			A_SpawnItemEx("FragP",-4,4,0,0,0,0,0,SXF_NOCHECKPOSITION);
			A_SpawnItemEx("FragP",0,4,0,0,0,0,0,SXF_NOCHECKPOSITION);
			A_SpawnItemEx("FragP",4,0,0,0,0,0,0,SXF_NOCHECKPOSITION);
			A_SpawnItemEx("FragP",4,4,0,0,0,0,0,SXF_NOCHECKPOSITION);
		}stop
	}
}


actor DecoPusher : IdleDummy
{
	states
	{
	spawn:
		TNT1 A 0
		TNT1 A 0 A_Jump(12,2,3)
		TNT1 A 0 A_SpawnItemEx("HDGoreBits",random(-4,4),random(-4,4),2,random(1,4),0,0,random(0,365),SXF_NOCHECKPOSITION) stop
		TNT1 A 0 A_SpawnItemEx("InnocentBarrel",random(-4,4),random(-4,4),0,random(1,4),0,0,random(0,365),SXF_NOCHECKPOSITION) stop
		TNT1 A 0 A_SpawnItemEx("InnocentFireCan",random(-4,4),random(-4,4),0,random(1,4),0,0,random(0,365),SXF_NOCHECKPOSITION) stop
	}
}
actor HDCasingBits : IdleDummy
{
	states
	{
	spawn:
		TNT1 A 0
		TNT1 A 0 A_SetAngle(random(0,365))
		TNT1 A 0 A_ChangeVelocity(frandom(-3,6),0,0,CVF_RELATIVE)

		TNT1 A 0 A_Jump(200,"shel")
		TNT1 A 0 A_Jump(196,"pis")
		goto lib
	shel:
		TNT1 A 0 A_CheckSight(1)
		stop
		TNT1 A 0 A_Jump(12,2)
		TNT1 A 0 A_Jump(256,2)
		TNT1 A 0 A_SpawnItemEx("HDFumblingShell",0,0,48,random(-2,2),random(-2,2),random(-2,2))
		TNT1 A 0 A_SpawnItemEx("HDSpentShell",0,0,48,random(-2,2),random(-2,2),random(-2,2))
		TNT1 A 0 A_SetTics(random(2,30))
		TNT1 A 0 A_Jump(190,"shel")
		stop
	pis:
		TNT1 A 0 A_CheckSight(1)
		stop
		TNT1 A 0 A_SpawnItemEx("HDSpent9mm",0,0,48,random(-2,2),random(-2,2),random(-2,2))
		TNT1 A 0 A_SetTics(random(1,20))
		TNT1 A 0 A_Jump(200,"pis")
		stop
	lib:
		TNT1 A 0 A_CheckSight(1)
		stop
		TNT1 A 0 A_SpawnItemEx("HDSpent7mm",0,0,48,random(-2,2),random(-2,2),random(-2,2))
		TNT1 A 0 A_SetTics(random(2,10))
		TNT1 A 0 A_Jump(190,"lib")
		stop
	}
}
actor HDGoreBits : RandomSpawner
{
	dropitem "DeadDemon" 126 1
	dropitem "DeadDoomImp" 126 1
	dropitem "DeadZombieMan" 96 1
	dropitem "DeadShotgunGuy" 96 1
	dropitem "DeadRifleman" 96 1
	dropitem "ReallyDeadRifleman" 46 1
	dropitem "ColonGibs" 256 4
	dropitem "Gibs" 256 6
	dropitem "SmallBloodPool" 256 8
	dropitem "BarrelGibs" 256 8
	dropitem "HDCasingBits" 256 32
	dropitem "HDFumbledShell" 256 8
	dropitem "HDSMGEmptyMag" 256 3
	dropitem "HDPistolEmptyMag" 256 3
	dropitem "LiberatorEmptyMag" 96 1
	dropitem "HD4mmMagEmpty" 256 8
}
actor HelmFrag : RandomSpawner replaces ArmorBonus
{
	dropitem "DecoPusher" 96 20
	dropitem "GreenHelmet" 96 2
	dropitem "HDFragGrenades" 72 1
	dropitem "LiberatorMag" 48 1
	dropitem "ZM66RifleMags" 48 1
	dropitem "BFGVileShard" 96 1
	dropitem "HDCellPacks" 48 1
	dropitem "PortableStimpack" 48 1
	dropitem "ClipBox" 48 1
	dropitem HDAB 48 1
}
actor BlueFrag : RandomSpawner replaces HealthBonus
{
	dropitem "DecoPusher" 96 20
	dropitem "BluePotion" 96 2
	dropitem "HDFragGrenades" 72 1
	dropitem "BFGVileShard" 96 1
	dropitem "HDPistolMag" 48 1
	dropitem "HDCellPacks" 72 1
	dropitem "PortableMedikit" 48 1
	dropitem "PortableStimpack" 48 1
	dropitem HDAB 48 1
}


// sticking oober spheres in here since they inherit from BluePotion

actor RedSphere : RandomSpawner replaces BlurSphere
{
	dropitem "SquadSummoner" 256 14
	dropitem "HDFragGrenades" 256 6
	dropitem "HDBlurSphere" 256 1
	dropitem "GreenArmor" 256 2
	dropitem "BluePotion" 256 2
}
actor BlueSphere : switchabledecoration replaces Soulsphere
{
	+floatbob
	+usespecial activation THINGSPEC_Activate|THINGSPEC_ThingTargets
	alpha 0.8 //renderstyle add
	scale 0.8
	states
	{
	spawn:
		SOUL ABCDCB random(2,7) bright light("SOULSPHERE")
		loop
	active:
	inactive:
		"----" A 0 A_ChangeVelocity(0,0,1)
		"----" A 0 A_NoGravity
		"----" A 0 A_PlaySound("misc/p_pkup")
		"----" A 0 A_Jump(256,"give")
	give:
		"----" A 0 A_GiveToTarget("RegenBlues",490)
	fade:
		"----" A 1 bright{
			A_SetScale(scalex*1.01);
			A_FadeOut(0.1);
			A_SpawnItemEx("HDGunSmoke",random(-4,4),random(-4,4),random(6,24),random(-1,1),random(-1,1),1,0,SXF_NOCHECKPOSITION);
		}loop
	}
}
actor BrownSphere : BlueSphere replaces Megasphere
{
	states{
	spawn:
		MEGA ABCD random(2,7) bright
		loop
	give:
		MEGA "#" 0{
			A_SetTranslucent(1,1);
			A_ChangeVelocity(0,0,1);
			A_NoGravity;
			A_PlaySound("misc/p_pkup");
			A_GiveToTarget("RegenBlues",490);
			A_GiveToTarget("SpiritualArmour");
			A_GiveToTarget("SpiritualArmour");
			A_GiveToTarget("GreenHelmet");
		}goto fade
	}
}
actor GreenSphere : BlueSphere replaces Invulnerabilitysphere
{
	+shootable
	renderstyle normal
	scale 0.6
	-floatbob
	-nogravity
	health 200
	mass 1000
	height 12
	radius 12
	states
	{
	spawn:
		PINV ABCD 6 bright
		loop
	death.telefrag:
	give:
		TNT1 A 0 A_GiveToTarget("HDInvuln",1)
		stop
	death:
		TNT1 A 0 A_SpawnItemEx("HDExplosion",4,-4,20,velx,vely,velz+1,0,SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS|SXF_ABSOLUTEMOMENTUM)
		TNT1 A 0 A_SpawnItemEx("HDExplosion",4,4,20,velx,vely,velz+1,0,SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS|SXF_ABSOLUTEMOMENTUM)
		TNT1 A 0 A_SpawnItemEx("HDExplosion",-4,-4,20,velx,vely,velz+1,0,SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS|SXF_ABSOLUTEMOMENTUM)
		TNT1 AA 0 A_SpawnItemEx("HDSmoke",random(-4,4),random(-4,4),2,0,0,random(1,3),0,SXF_NOCHECKPOSITION)
		TNT1 A 2 A_SpawnItemEx("HDExplosion",-4,4,20,velx,vely,velz+1,0,SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS|SXF_ABSOLUTEMOMENTUM)
		TNT1 A 0 A_Quake(3,26,0,220,none)
		TNT1 A 0 A_SpawnItemEx("ArchAngel",0,0,0,velx,vely,velz,0,SXF_NOCHECKPOSITION|SXF_ABSOLUTEMOMENTUM)
		TNT1 AAAAA 2 A_SpawnItemEx("HDSmoke",random(-4,4),random(-4,4),2,velx,vely,velz+random(1,3),0,SXF_NOCHECKPOSITION|SXF_ABSOLUTEMOMENTUM)
		TNT1 AAAAA 4 A_SpawnItemEx("HDSmoke",random(-4,4),random(-4,4),2,velx,vely,velz+random(1,3),0,SXF_NOCHECKPOSITION|SXF_ABSOLUTEMOMENTUM)
		stop
	}
}
actor HDInvuln : ActionItem
{
	states
	{
	pickup:
		TNT1 A 0 A_Quake(3,26,0,220,none)
		TNT1 A 0 A_GiveInventory("HDInvulnP",1)
		TNT1 A 0 A_RadiusGive ("HDFireStarter", 128, RGF_PLAYERS|RGF_MONSTERS|RGF_OBJECTS|RGF_CORPSES, 1)
		TNT1 A 0 A_SpawnItemEx("HDExplosion",4,-4,20,velx,vely,velz+1,0,SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS|SXF_ABSOLUTEMOMENTUM)
		TNT1 A 0 A_SpawnItemEx("HDExplosion",4,4,20,velx,vely,velz+1,0,SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS|SXF_ABSOLUTEMOMENTUM)
		TNT1 A 0 A_SpawnItemEx("HDExplosion",-4,-4,20,velx,vely,velz+1,0,SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS|SXF_ABSOLUTEMOMENTUM)
		TNT1 A 0 A_SpawnItemEx("HDExplosion",-4,4,20,velx,vely,velz+1,0,SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS|SXF_ABSOLUTEMOMENTUM)
		MISX AAA 0 A_SpawnItemEx("HDSmokeChunk", 0,0,0,velx+random(-12,12),vely+random(-12,12),velz+random(4,16),0,160,32)
		TNT1 AAAAA 0 A_GiveInventory("HDFireStarter",1)
		stop
	}
}
ACTOR HDInvulnP : InvulnerabilitySphere
{
	Powerup.Duration -90
	+inventory.persistentpower
}




//-------------------------------------------------
//    Backpack Replacements
//-------------------------------------------------

actor HDBackpack:Ammo{
	inventory.maxamount 1 ammo.backpackmaxamount 1 ammo.backpackamount 0
}
actor HDEBPPK:HDUPK replaces HDBackpack{
	states{
	spawn:
		BPAK B -1 A_Stop
	give:
		BPAK B 0{
			A_PlaySound("weapons/pocket");
			if(countinv("HDBackpack",AAPTR_TARGET)<1){
				A_GiveToTarget("HDBackpack");
				return state("null");
			}return state("spawn");
		}
	}
}
actor HDBPPK:HDUPK replaces Backpack{
	states{
	spawn:
		BPAK A 0 A_Stop
		BPAK A -1{
			if(!countinv("Clip")){
				A_GiveInventory("Clip");
				A_SetTics(1);
			}
		}
		BPAK A 0 A_Jump(64,1,2,3,4,5) loop
		BPAK A 0 A_SpawnItem("BossRifleSpawner",9) loop
		BPAK A 0 A_SpawnItem("BloopMapPickup",9) loop
		BPAK A 0 A_SpawnItem("LiberatorRifle",9) loop
		BPAK A 0 A_SpawnItem("LiberatorNoGL",9) loop
		BPAK A 0 A_SpawnItem("HERPUsable",9) loop
	give:
		TNT1 A 0{
			A_GiveInventory("Timer",7);
			A_PlaySound("weapons/pocket");
			if(countinv("HDBackpack",AAPTR_TARGET)>0){  
				A_SpawnItemEx("HDEBPPK",0,0,0,-3,0,1,0,SXF_NOCHECKPOSITION);
				return state("dropstuff");
			}else{A_GiveToTarget("HDBackpack");}
			return state("givestuff");
		}
	dropstuff:
		TNT1 A 0 A_JumpIfInventory("Timer",1,1)
		stop
		TNT1 A 1 A_ClearTarget
		TNT1 A 0 A_TakeInventory("Timer",1)
		TNT1 A 0 A_Jump(256,1,2,3,4,5,6,7,8,9,10,11,12,13,14,15,16,17,18,19,20)
	d1: TNT1 A 0 A_SpawnItemEx("GreenHelmet",0,0,0,-3,0,1,random(-90,90),SXF_NOCHECKPOSITION) goto dropstuff
	d2: TNT1 A 0{
		A_SpawnItemEx("GreenArmor",0,0,0,-3,0,1,random(-90,90),SXF_NOCHECKPOSITION);
		}goto dropstuff
	d3: TNT1 A 0{
		A_SpawnItemEx("BossClip",0,0,0,-3,0,1,random(-90,90),SXF_NOCHECKPOSITION);
		A_SpawnItemEx("BossClip",0,0,0,-3,0,1,random(-90,90),SXF_NOCHECKPOSITION);
		A_SpawnItemEx("BossClip",0,0,0,-3,0,1,random(-90,90),SXF_NOCHECKPOSITION);
		}goto dropstuff
	d4: TNT1 A 0{
		A_SpawnItemEx("Clip",0,0,0,-3,0,1,random(-90,90),SXF_NOCHECKPOSITION);
		A_SpawnItemEx("Clip",0,0,0,-3,0,1,random(-90,90),SXF_NOCHECKPOSITION);
		}goto dropstuff
	d5: TNT1 A 0 A_SpawnItemEx("ShellPickup",0,0,0,-3,0,1,random(-90,90),SXF_NOCHECKPOSITION) goto dropstuff
	d6: TNT1 A 0 A_SpawnItemEx("RocketAmmo",0,0,0,-3,0,1,random(-90,90),SXF_NOCHECKPOSITION) goto dropstuff
	d7: TNT1 A 0 A_SpawnItemEx("HDCellPacks",0,0,0,-3,0,1,random(-90,90),SXF_NOCHECKPOSITION) goto dropstuff
	d8: TNT1 A 0 A_SpawnItemEx("BlurSphere",0,0,0,-3,0,1,random(-90,90),SXF_NOCHECKPOSITION) goto dropstuff
	d9: TNT1 A 0 A_SpawnItemEx("BluePotion",0,0,0,-3,0,1,random(-90,90),SXF_NOCHECKPOSITION) goto dropstuff
	d10: TNT1 A 0 A_SpawnItemEx("Medikit",0,0,0,-3,0,1,random(-90,90),SXF_NOCHECKPOSITION) goto dropstuff
	d11: TNT1 A 0 A_SpawnItemEx("PortableBerserkPack",0,0,0,-3,0,1,random(-90,90),SXF_NOCHECKPOSITION) goto dropstuff
	d12: TNT1 A 0{
		A_SpawnItemEx("Stimpack",0,0,0,-3,0,1,random(-90,90),SXF_NOCHECKPOSITION);
		A_SpawnItemEx("Stimpack",0,0,0,-3,0,1,random(-90,90),SXF_NOCHECKPOSITION);
		}goto dropstuff
	d13: TNT1 AAA 0 A_SpawnItemEx("Medikit",0,0,0,-3,0,1,random(-90,90),SXF_NOCHECKPOSITION) goto dropstuff
	d14: TNT1 A 0 A_SpawnItemEx("Radsuit",0,0,0,-3,0,1,random(-90,90),SXF_NOCHECKPOSITION) goto dropstuff
	d15: TNT1 A 0 A_SpawnItemEx("Infrared",0,0,0,-3,0,1,random(-90,90),SXF_NOCHECKPOSITION) goto dropstuff
	d16: TNT1 A 0 A_SpawnItemEx("HDSMG",0,0,0,-3,0,1,random(-90,90),SXF_NOCHECKPOSITION) goto dropstuff
	d17: TNT1 A 0 A_SpawnItemEx("HDPistol",0,0,0,-3,0,1,random(-90,90),SXF_NOCHECKPOSITION) goto dropstuff
	d18: TNT1 A 0 A_SpawnItemEx("BrontornisSpawner",0,0,0,-3,0,1,random(-90,90),SXF_NOCHECKPOSITION) goto dropstuff
	d19: TNT1 A 0 A_SpawnItemEx("HDFragGrenadeAmmo",0,0,0,-3,0,1,random(-90,90),SXF_NOCHECKPOSITION) goto dropstuff
	d20: TNT1 A 0 A_SpawnItemEx("HEATAmmo",0,0,0,-3,0,1,random(-90,90),SXF_NOCHECKPOSITION) goto dropstuff

	givestuff:
		TNT1 A 0 A_JumpIfInventory("Timer",1,1)
		stop
		TNT1 A 0 A_TakeInventory("Timer",1)
		TNT1 A 0 A_Jump(256,1,2,3,4,5,6,7,8,9,10,11,12,13,14,15,16,17,18,19,20,21,22,23,24,25,26,27,28,29,30,31,32,33)
	g1: TNT1 A 0 A_GiveToTarget("HDCellPacks") goto givestuff
	g2: TNT1 A 0 A_GiveToTarget("HDCellAmmo",random(1,20)) goto givestuff
	g3: TNT1 A 0 A_GiveToTarget("HDSMGMag") goto givestuff
	g4: TNT1 A 0 A_GiveToTarget("HDPistolMag",random(1,2)) goto givestuff
	g5: TNT1 A 0 A_GiveToTarget("HDPistolAmmo",random(10,30)) goto givestuff
	g6: TNT1 A 0 A_GiveToTarget("HDShellAmmo",random(4,8)) goto givestuff
	g7: TNT1 A 0 A_GiveToTarget("ZM66RifleMags") goto givestuff
	g8: TNT1 A 0 A_GiveToTarget("FourMilAmmo",random(1,50)) goto givestuff
	g9: TNT1 A 0 A_GiveToTarget("HDRocketAmmo") goto givestuff
	g10: TNT1 A 0 A_GiveToTarget("HEATAmmo") goto givestuff
	g11: TNT1 A 0 A_GiveToTarget("LiberatorMag") goto givestuff
	g12: TNT1 A 0 A_GiveToTarget("BossClip",random(1,3)) goto givestuff
	g13: TNT1 A 0 A_GiveToTarget("SevenMilAmmo",random(10,30)) goto givestuff
	g14: TNT1 A 0 A_GiveToTarget("HDCellPackEmpty") goto givestuff
	g15: TNT1 A 0 A_GiveToTarget("BrontornisRound",random(3,6)) goto givestuff
	g16: TNT1 A 0 A_GiveToTarget("HDFragGrenadeAmmo",random(1,2)) goto givestuff
	g17: TNT1 A 0{
		A_GiveToTarget("HDSecondPistol");
		}goto givestuff
	g18: TNT1 A 0{
		A_GiveToTarget("HDSMG");
		}goto givestuff
	g19: TNT1 A 0{
		A_GiveToTarget("Brontornis");
		A_GiveToTarget("BrontornisRound",random(1,3));
		}goto givestuff
	g20: TNT1 A 0 A_GiveToTarget("PortableStimpack") goto givestuff
	g21: TNT1 A 0 A_GiveToTarget("PortableMedikit",random(1,2)) goto givestuff
	g22: TNT1 A 0 A_GiveToTarget("PortableBerserkPack",random(1,2)) goto givestuff
	g23: TNT1 A 0 A_GiveToTarget("PortableLiteAmp") goto givestuff
	g24: TNT1 A 0 A_GiveToTarget("PortableRadsuit") goto givestuff
	g25: TNT1 A 0 A_GiveToTarget("Medikit",3) goto givestuff
	g26: TNT1 A 0 A_GiveToTarget("DERPUsable") goto givestuff
	g27: TNT1 A 0 A_GiveToTarget("BluePotion") goto givestuff
	g28: TNT1 A 0 A_GiveToTarget("SquadSummoner") goto givestuff
	g29: TNT1 A 0 A_GiveToTarget("HDIEDKit",random(4,8)) goto givestuff
	g30: TNT1 A 0 A_SpawnItemEx("GreenHelmet",0,0,0,-3,0,1,random(-4,4),SXF_NOCHECKPOSITION) goto givestuff
	g31: TNT1 A 0 A_SpawnItemEx("GreenArmourPickup",0,0,0,-3,0,1,random(-4,4),SXF_NOCHECKPOSITION) goto givestuff
	g32: TNT1 A 0 A_SpawnItemEx("BlueArmourPickup",0,0,0,-3,0,1,random(-4,4),SXF_NOCHECKPOSITION) goto givestuff
	g33: TNT1 A 0 A_SpawnItemEx("BlackArmourPickup",0,0,0,-3,0,1,random(-4,4),SXF_NOCHECKPOSITION) goto givestuff
	}
}
