// ------------------------------------------------------------
// Former Human Sergeant
// ------------------------------------------------------------
class Jackboot:HideousShotgunGuy{default{accuracy 1;}}
class JackAndJillboot:HideousShotgunGuy{default{accuracy 2;}}
class UndeadJackbootman:HideousShotgunGuy{default{accuracy 3;}}
class HideousShotgunGuy:ShotgunGuy replaces ShotgunGuy{
	default{
		dropitem "";
		health 80;
		gibhealth 100;
		speed 10;
		height 52;
		radius 12;
		decal "BulletScratch";
		attacksound "";
		meleesound "weapons/smack";
		meleedamage 4;
		maxtargetrange 4000;
		painchance 200;
		accuracy 0;

		//placeholder
		obituary "%o was brutally pumped full of a shotgun sergeant's hot, manly lead.";
		hitobituary "%o was whipped and dominated by fifty shades of shotgun guy.";
	}
	override string getobituary(actor victim,actor inflictor,name mod,bool playerattack){
		bool shotdead=false;
		string ob="%o was brutally pumped full of a shotgun sergeant's hot, manly lead.";
		if(inflictor is "HDBullet"||inflictor==victim)shotdead=true;
		if(shotdead){
			if(wep>0)ob="%o was doubly penetrated with a shotgun sergeant's hot, manly lead.";
			else if(wep<0)ob="%o was penetrated by the Tyrant's jack-booted thugs.";
		}else{
			if(wep<0)ob="%o was pounded by the Tyrant's jack-booted thugs.";
			else ob="%o was whipped and dominated by fifty shades of shotgun guy.";
		}
		return ob;
	}
	bool hasdropped;
	bool semi;
	int gunloaded;
	int gunspent;
	int wep;
	int turnamount;
	double shotspread;
	override void postbeginplay(){
		super.postbeginplay();
		hasdropped=0;

		//-1 zm66, 0 sg, 1 ssg
		if(!accuracy) wep=random(0,1)-random(0,1);
		else if(accuracy==1)wep=0;
		else if(accuracy==2)wep=1;
		else if(accuracy==3)wep=-1;
		if(wep<0){
			sprite=GetSpriteIndex("PLAYA1");
			A_SetTranslation("HattedJackboot");
			gunloaded=50;
		}else{
			sprite=GetSpriteIndex("SPOSA1");
			A_SetTranslation("ShotgunGuy");
			gunloaded=wep?2:8;
		}
		semi=randompick(0,0,1);
		hdmobster.spawnmobster(self);
	}
	void noblockwepdrop(){
		A_NoBlocking();
		if(hasdropped){
			if(wep<0)A_DropItem("ZM66RifleMags",0,96);
			else A_DropItem("ShellPickup",0,200);
		}else{
			A_DropItem("HDPistol",0,16);
			hasdropped=true;
			hdweapon wp;
			if(wep==-1){
				wp=hdweapon(spawn("ZM66AssaultRifle",(pos.x,pos.y,pos.z+40)));
				wp.weaponstatus[0]=
					ZM66F_NOLAUNCHER|(randompick(0,1,1,1,1)*ZM66F_CHAMBER);
				wp.weaponstatus[ZM66S_MAG]=gunloaded;
				wp.weaponstatus[ZM66S_AUTO]=2;
				wp.weaponstatus[ZM66S_ZOOM]=random(0,27);

				gunloaded=50;
			}
			if(wep==0){
				wp=hdweapon(spawn("Hunter",(pos.x,pos.y,pos.z+40)));
				if(!semi)wp.weaponstatus[0]|=HUNTF_AUTO;
				if(gunspent)wp.weaponstatus[HUNTS_CHAMBER]=1;
				else if(gunloaded>0){
					wp.weaponstatus[HUNTS_CHAMBER]=2;
					gunloaded--;
				}
				if(gunloaded>0)wp.weaponstatus[HUNTS_TUBE]=gunloaded;
				wp.weaponstatus[HUNTS_SIDESADDLE]=random(0,12);

				gunloaded=8;
			}
			if(wep==1){
				wp=hdweapon(spawn("Slayer",(pos.x,pos.y,pos.z+40)));
				if(gunloaded==2)wp.weaponstatus[SLAYS_CHAMBER2]=2;
				else if(gunspent==2)wp.weaponstatus[SLAYS_CHAMBER2]=1;
				if(gunloaded>0)wp.weaponstatus[SLAYS_CHAMBER1]=2;
				else if(gunspent>0)wp.weaponstatus[SLAYS_CHAMBER1]=1;
				wp.weaponstatus[SLAYS_SIDESADDLE]=random(0,12);

				gunloaded=2;
			}
			wp.vel=vel+(frandom(-2,2),frandom(-2,2),1);
		}
		gunspent=0;
		if(wep==-1){
			gunloaded=50;
		}
		if(wep==0){
			gunloaded=8;
		}
		if(wep==1){
			gunloaded=2;
		}
	}
	states{
	spawn:
		SPOS A 0 nodelay A_JumpIf(wep>=0,2);
		PLAY A 0;
		#### EEEEEE 1{
			A_Look();
			A_Recoil(frandom(-1.,1.));
			A_SetTics(random(1,10));
		}
		#### B 0 A_Jump(132,2,5,5,5,5);
		#### B 8{
			if(!random(0,1)){
				if(!random(0,4)){
					setstatelabel("spawnstretch");
				}else{
					if(bambush)setstatelabel("spawnstill");
					else setstatelabel("spawnwander");
				}
			}else A_Recoil(random(-1,1)*0.2);
		}loop;
	spawnstretch:
		#### G 1{
			A_Recoil(frandom(-0.4,0.4));
			A_SetTics(random(30,80));
		}
		#### A 0 A_PlaySound("grunt/active",CHAN_VOICE);
		goto spawn;
	spawnstill:
		#### C 0{
			A_Look();
			A_Recoil(random(-1,1)*0.4);
		}
		#### CD 5{angle+=random(-4,4);}
		#### A 0{
			A_Look();
			if(random(0,7))A_PlaySound("grunt/active",CHAN_VOICE);
		}
		#### AB 5{angle+=random(-4,4);}
		#### B 1 A_SetTics(random(10,40));
		goto spawn;
	spawnwander:
		#### CD 5{hdmobai.wander(self,false);}
		#### A 0{if(random(0,7))A_PlaySound("grunt/active",CHAN_VOICE);}
		#### AB 5{hdmobai.wander(self,false);}
		#### A 0 A_Jump(64,"spawn");
		loop;

	see:
		#### A 0{
			if(gunloaded<1)setstatelabel("reload");
			else if(!wep&&gunspent>0)setstatelabel("chambersg");
			if(health<25)damagemobj(self,target,1,"bleedout",DMG_NO_PAIN);
		}
		#### AABBCCDD 2{hdmobai.chase(self);}
		#### A 0 A_JumpIfTargetInLOS("see");
		#### A 0 A_Jump(16,"roam");
		loop;
	roam:
		#### A 0 A_Jump(60,"roam2");
	roam1:
		#### E 4{bmissileevenmore=true;}
		#### EEEEEEEEEEEEEEEE 1 A_Chase("melee","turnaround",CHF_DONTMOVE);
		#### A 0{bmissileevenmore=false;}
		#### A 0 A_Jump(60,"roam");
	roam2:
		#### A 0 A_Jump(8,"see");
		#### A 0{healthing(random(1,2));}
		#### AA 3{hdmobai.chase(self);}
		#### A 0 A_Chase("melee","turnaround",CHF_DONTMOVE);
		#### BBCC 3{hdmobai.wander(self,false);}
		#### A 0 A_Chase("melee","turnaround",CHF_DONTMOVE);
		#### DD 3{hdmobai.chase(self);}
		#### A 0 A_Jump(200,"Roam");
		#### A 0{
			A_PlaySound(seesound,CHAN_VOICE);
			A_AlertMonsters();
		}
		#### A 0 A_JumpIfTargetInLOS("see");
		loop;
	turnaround:
		#### A 0 A_FaceTarget(15,0);
		#### E 2 A_JumpIfTargetInLOS("missile2",40);
		#### A 0 A_FaceTarget(15,0);
		#### E 2 A_JumpIfTargetInLOS("missile2",40);
		#### AABBCCDD 2{hdmobai.chase(self);}
		goto see;

	missile:
		#### A 0 A_JumpIfTargetInLOS(3,120);
		#### CD 2 A_FaceTarget(90);
		#### E 1 A_SetTics(random(4,10)); //when they just start to aim,not for followup shots!
		#### A 0 A_CheckLOF("see",
			CLOFF_JUMPNONHOSTILE|CLOFF_SKIPTARGET|
			CLOFF_JUMPOBJECT|CLOFF_MUSTBESOLID|CLOFF_SKIPENEMY,
			0,0,0,0,44,0
		);
	missile2:
		#### A 0{
			double dist=distance3d(target);
			if(dist<300){
				turnamount=40;
			}else if(dist<800){
				turnamount=30;
			}else{
				turnamount=20;
			}
		}//fallthrough to turntoaim
	turntoaim:
		#### E 2 A_FaceTarget(turnamount,turnamount);
		#### A 0 A_JumpIfTargetInLOS(1);
		goto see;
		#### A 0 A_JumpIfTargetInLOS(1,10);
		loop;
		#### A 0 A_FaceTarget(turnamount,turnamount);
		#### E 1A_SetTics(random(0,10-turnamount/10));
		#### E 0{
			if(
				gunloaded<1
			){
				setstatelabel("ohforfuckssake");
				return;
			}
			shotspread=frandom(turnamount/14,turnamount/5);
			setstatelabel("shoot");
		}
	shoot:
		#### A 0{
			if(gunloaded<1){
				setstatelabel("ohforfuckssake");
				return;
			}
			if(wep==-1)setstatelabel("shootzm66");
			else if(wep==1)setstatelabel("shootssg");
			else setstatelabel("shootsg");
		}


	shootzm66:
		#### F 0{
			gunspent=0;
			angle+=frandom(-shotspread,shotspread);
			pitch+=frandom(-shotspread,shotspread);
		}
	shootzm662:
		#### F 1 bright light("SHOT"){
			if(!random(0,999)){
				A_PlaySound("weapons/rifleclick",CHAN_WEAPON);
				gunloaded=-gunloaded;
				setstatelabel("ohforfuckssake");
				return;
			}

			angle+=frandom(-0.5,0.5);
			pitch+=frandom(-0.5,0.5);

			A_PlaySound("weapons/rifle",CHAN_WEAPON);

			gunspent++;
			gunloaded--;
			actor p=spawn("HDBullet426",pos+(0,0,height-6));
			p.target=self;p.angle=angle;p.pitch=pitch;
			p.vel+=self.vel;
		}
		#### E 1{
			if(gunspent<3&&gunloaded>0)setstatelabel("shootzm662");
			else A_SetTics(random(4,12));
		}
		#### E 0 A_Jump(127,"see");
		#### E 0 A_SpidRefire();
		goto turntoaim;

	shootssg:
		#### F 1 bright light("SHOT"){
			if(vel.length()>30){
				setstatelabel("see");
				return;
			}
			angle+=frandom(-shotspread,shotspread);
			pitch+=frandom(-shotspread,shotspread);

			A_PlaySound("weapons/slayersingle",CHAN_WEAPON);
			if(gunloaded>1&&!random(0,5)){
				//both barrels
				A_PlaySound("weapons/slayersingle",5);
				gunspent=2;
				gunloaded=0;
				actor p=spawn("HDBullet00bf2",pos+(0,0,height-6));
				p.target=self;p.angle=angle;p.pitch=pitch;
				p.vel+=self.vel;
			}else{
				//single barrel
				//NOTE: DELETE HDBULLET00BF WHEN DONE
				gunspent++;
				gunloaded--;
				class<actor> whichbf="HDBullet00bfl";
				if(gunspent)whichbf="HDBullet00bfr";
				actor p=spawn(whichbf,pos+(0,0,height-6));
				p.target=self;p.angle=angle;p.pitch=pitch;
				p.vel+=self.vel;
			}
		}
		#### E 1 A_SetTics(random(2,4));
		#### E 0 A_Jump(192,"see");
		#### E 0 A_SpidRefire();
		goto turntoaim;

	shootsg:
		#### F 1 bright light("SHOT"){
			if(gunspent>0){
				setstatelabel("chambersg");
				return;
			}else if(vel.length()>20){
				setstatelabel("see");
				return;
			}
			angle+=frandom(-shotspread,shotspread);
			pitch+=frandom(-shotspread,shotspread);

			A_PlaySound("weapons/hunter",CHAN_WEAPON);

			gunspent++;
			gunloaded--;
			actor p=spawn("HDBullet00b",pos+(0,0,height-6));
			p.target=self;p.angle=angle;p.pitch=pitch;
			p.vel+=self.vel;
		}
		#### E 3{
			if(semi&&random(0,5)){
				A_SetTics(0);
				gunspent=0;
				A_SpawnItemEx("HDSpentShell",
					cos(pitch)*8,0,height-7-sin(pitch)*8,
					vel.x+cos(pitch)*cos(angle-random(86,90))*6,
					vel.y+cos(pitch)*sin(angle-random(86,90))*6,
					vel.z+sin(pitch)*random(5,7),0,
					SXF_ABSOLUTEMOMENTUM|SXF_NOCHECKPOSITION|SXF_TRANSFERPITCH
				);
			}
		}
		#### E 1{
			if(gunspent)setstatelabel("chambersg");
			else A_SetTics(random(3,8));
		}
		#### E 0 A_Jump(127,"see");
		#### E 0 A_SpidRefire();
		goto turntoaim;
	chambersg:
		#### E 8{
			if(gunspent){
				A_SetTics(random(3,10));
				A_PlaySound("weapons/shotgr",5);
				gunspent=0;
				A_SpawnItemEx("HDSpentShell",
					cos(pitch)*8,0,height-7-sin(pitch)*8,
					vel.x+cos(pitch)*cos(angle-random(86,90))*6,
					vel.y+cos(pitch)*sin(angle-random(86,90))*6,
					vel.z+sin(pitch)*random(5,7),0,
					SXF_ABSOLUTEMOMENTUM|SXF_NOCHECKPOSITION|SXF_TRANSFERPITCH
				);
			}
		}
		#### E 1 A_SetTics(random(3,8));
		#### E 0 A_Jump(127,"see");
		#### E 0 A_SpidRefire();
		goto turntoaim;

	ohforfuckssake:
		#### E 6;
	reload:
		#### A 0{
			if(wep==-1)setstatelabel("reloadzm66");
			else if(wep==1)setstatelabel("reloadssg");
			else setstatelabel("reloadsg");
		}


	reloadzm66:
		#### A 0{bfrightened=true;}
		#### AA 1{hdmobai.chase(self,"melee",null);}
		#### A 0 A_PlaySound("weapons/rifleclick2",CHAN_WEAPON);
		#### BCD 2{hdmobai.chase(self,"melee",null);}
		#### A 2{
			hdmobai.wander(self,true);
			if(gunspent==999)return;

			A_PlaySound("weapons/rifleload");
			if(!gunloaded)A_SpawnProjectile("HD4mmMagEmpty",38,0,random(90,120));
			else{
				int garbage;actor mmm;
				[garbage,mmm]=A_SpawnItemEx("HD4mmMag",
					12,0,40,1,0,-1,0,SXF_SETTARGET
				);
				mmm.stamina=abs(gunloaded);
				gunspent=999;
			}
		}
		#### BCD 2{hdmobai.chase(self,"melee",null);}
		#### A 4 A_PlaySound("weapons/pocket");
		#### BC 4{hdmobai.wander(self,true);}
		#### E 6 A_PlaySound("weapons/rifleload");
		#### E 2{
			A_PlaySound("weapons/rifleclick2");
			gunloaded=50;
			gunspent=0;
			bfrightened=false;
			hdmobai.wander(self,true);
		}
		#### CCBB 2{hdmobai.chase(self,"melee",null);}
		goto turntoaim;

	reloadssg:
		#### E 2;
		#### E 2 A_PlaySound("weapons/sshoto",6);
		#### E 0{
			while(gunspent>0){
				gunspent--;
				A_SpawnItemEx("HDSpentShell",
					cos(pitch)*5,-1,height-7-sin(pitch)*5,
					cos(pitch-45)*cos(angle)*random(1,4)+vel.x,
					cos(pitch-45)*sin(angle)*random(1,4)+vel.y,
					-sin(pitch-45)*random(1,4)+vel.z,0,
					SXF_ABSOLUTEMOMENTUM|SXF_NOCHECKPOSITION|SXF_TRANSFERPITCH
				);
			}
		}

		#### EEDD 1{hdmobai.chase(self,"melee",null);}
		#### DAAB 3{hdmobai.chase(self,"melee",null);}
		#### B 1 A_PlaySound("weapons/sshotl",6);
		#### CCD 4;
		#### E 6{
			A_PlaySound("weapons/sshotc",6);
			gunloaded=2;
		}
		goto see;

	reloadsg:
		#### A 0{bfrightened=true;}
		#### AA 1{hdmobai.chase(self,"melee",null);}
		#### A 0 A_PlaySound("weapons/huntopen",CHAN_WEAPON);
		#### BCDA 2{hdmobai.chase(self,"melee",null);}
	reloadsg2:
		#### BB 3{hdmobai.chase(self,null,null,flee:true);}
		#### B 0{
			gunloaded++;
			A_PlaySound("weapons/huntreload",CHAN_WEAPON);
			if(gunloaded>=8)setstatelabel("reloadsgend");
		}
		#### CC 3{hdmobai.chase(self,null,null,flee:true);}
		#### C 0{
			gunloaded++;
			A_PlaySound("weapons/huntreload",CHAN_WEAPON);
			if(gunloaded>=8)setstatelabel("reloadsgend");
		}
		#### DD 3{hdmobai.chase(self,null,null,flee:true);}
		#### D 0{
			gunloaded++;
			A_PlaySound("weapons/huntreload",CHAN_WEAPON);
			if(gunloaded>=8)setstatelabel("reloadsgend");
		}
		#### A 0 A_PlaySound("weapons/pocket",CHAN_BODY);
		#### AABBCCDDAA 2{hdmobai.chase(self,null,null,flee:true);}
		loop;
	reloadsgend:
		#### A 0{bfrightened=false;}
		#### BBCCDD 2{hdmobai.chase(self,null,null);}
		#### A 0 A_PlaySound("weapons/huntopen",CHAN_WEAPON);
		#### EEE 1 A_Chase("melee","missile",CHF_DONTMOVE);
		goto see;

	melee:
		#### C 6 A_FaceTarget();
		#### D 4;
		#### E 4 A_CustomMeleeAttack(
			random(5,25),"weapons/smack","","none",randompick(0,0,0,1)
		);
		#### E 3 A_JumpIfCloser(64,2);
		#### E 3 A_FaceTarget();
		goto missile;
		#### A 3;
		goto see;
	pain:
		#### G 3 A_Jump(12,1);
		#### G 3{
			A_Pain();
			if(!random(0,7))A_AlertMonsters();
		}
		#### G 0{
			if(target&&distance3d(target)<100)setstatelabel("see");
			bfrightened=true;
		}
		#### ABCD 2{hdmobai.chase(self);}
		#### G 0{bfrightened=false;}
		goto see;

	death.bleedout:
		#### H 5{
			{hdmobai.corpseflags(self);}
			actor ttt=spawn("tempshield",pos);
			ttt.vel=vel;ttt.master=self;
		}
		#### I 5{
			if(!random(0,2))A_PlaySound("grunt/bleedout",CHAN_VOICE);
		}
		goto deathpostscream;
	death:
		#### H 5{
			{hdmobai.corpseflags(self);}
			actor ttt=spawn("tempshield",pos);
			ttt.vel=vel;ttt.master=self;
		}
		#### I 5 A_Scream();
	deathpostscream:
		#### J 5{
			actor ttt=spawn("tempshield2",pos);
			ttt.vel=vel;ttt.master=self;
			noblockwepdrop();
		}
		#### K 5;
	dead:
		#### K 3 canraise{if(abs(vel.z)<2.)frame++;}
		#### L 5 canraise{if(abs(vel.z)>=2.)setstatelabel("dead");}
		wait;
	xdeathbrewtlelulz:
		#### M 5{
			bshootable=false;
			A_GiveInventory("IsGibbed");
		}
		#### N 5{
			spawn("MegaBloodSplatter",pos+(0,0,34));
			A_XScream();
		}
		#### OPQRST 5;
		goto xdead;
	xdeath:
		#### M 5{
			bshootable=false;
			A_GiveInventory("IsGibbed");
		}
		#### N 5{
			spawn("MegaBloodSplatter",pos+(0,0,34));
			A_XScream();
		}
		#### O 0 noblockwepdrop();
		#### OP 5 spawn("MegaBloodSplatter",pos+(0,0,34));
		#### QRST 5;
		goto xdead;
	xdead:
		#### T 3 canraise{if(abs(vel.z)<2.)frame++;}
		#### U 5 canraise{if(abs(vel.z)>=2.)setstatelabel("xdead");}
		wait;
	raise:
		#### A 0 {hdmobai.corpseflags(self);}
		#### A 0 A_JumpIfInventory("IsGibbed",1,"RaiseGibbed");
		#### L 4 spawn("MegaBloodSplatter",pos+(0,0,34));
		#### LK 6;
		#### JIH 4;
		goto see;
	raisegibbed:
		#### U 12{
			A_TakeInventory("IsGibbed");
			spawn("MegaBloodSplatter",pos+(0,0,34));
		}
		#### T 8;
		#### SRQ 6;
		#### PONM 4;
		#### L 0 A_Die("Ungibbed");
	death.ungibbed:
		#### H 5{
			{hdmobai.corpseflags(self);}
			actor ttt=spawn("tempshield",pos);
			ttt.vel=vel;ttt.master=self;
		}
		#### I 5;
		#### J 5{
			actor ttt=spawn("tempshield2",pos);
			ttt.vel=vel;ttt.master=self;
			A_NoBlocking();
		}
		#### K 5;
		goto dead;
	}
}
