// ------------------------------------------------------------
// BFG9k
// ------------------------------------------------------------
class BFG9K:HDWeapon replaces BFG9000{
	default{
		//$Category "Weapons/Hideous Destructor"
		//$Title "BFG 9k"
		//$Sprite "BFUGA0"

		weapon.selectionorder 91;
		weapon.slotnumber 7;
		weapon.kickback 200;
		weapon.bobrangex 0.4;
		weapon.bobrangey 1.1;
		weapon.bobspeed 1.3;
		weapon.bobstyle "normal";
		scale 0.7;
		inventory.pickupmessage "You got the BFG 9000! Oh yes.";
	}
	//BFG9k.Spark(self,4);
	//BFG9k.Spark(self,4,height-10);
	static void Spark(actor caller,int sparks=1,int sparkheight=10){
		actor a;vector3 spot;
		vector3 origin=caller.pos+(0,0,sparkheight);
		double spp;double spa;
		for(int i=0;i<sparks;i++){
			spp=caller.pitch+frandom(-20,20);
			spa=caller.angle+frandom(-20,20);
			spot=random(32,57)*(cos(spp)*cos(spa),cos(spp)*sin(spa),-sin(spp));
			a=caller.spawn("BFGSpark",origin+spot);
			a.vel+=caller.vel*0.9-spot*0.03;
		}
	}
	override void doeffect(){
		if(hdplayerpawn(owner)){
			if(weaponstatus[0]&BFGF_STRAPPED&&(owner&&owner.health>0))buntossable=true;    
			else buntossable=false;
			if(
				owner.player&&owner.player.readyweapon==self&&
				!(hdplayerpawn(owner).gunbraced)&&
				!(weaponstatus[0]&BFGF_STRAPPED)&&
				owner.pitch<10
			)hdplayerpawn(owner).A_MuzzleClimb((frandom(-0.05,0.05),0.1),(0,0),(0,0),(0,0));
		}
	}
	states{
	altfire:
	togglestrap:
		BFGG A 3{
			A_GiveInventory("WeaponBusy");
			if(invoker.weaponstatus[0]&BFGF_STRAPPED){
				A_SetTics(6);
				A_PlaySound("weapons/rifleclick",CHAN_WEAPON);
			}
		}
		BFGG AA 1 A_Lower(3);
		BFGG AAA 1 A_Lower(5);
		BFGG AA 2 A_Lower(3);
		BFGG A 4{
			A_PlaySound("weapons/shotgr",5);
			if(!(invoker.weaponstatus[0]&BFGF_STRAPPED)){
				A_SetTics(6);
				A_PlaySound("weapons/rifleclick",CHAN_WEAPON);
			}
			A_SetBlend("00 00 00",1,6,"00 00 00");
			invoker.weaponstatus[0]^=BFGF_STRAPPED;
			if(invoker.buntossable)invoker.buntossable=false;
				else invoker.buntossable=true;
		}
		BFGG AA 2 A_Raise(3);
		BFGG AAA 1 A_Raise(5);
		BFGG AA 1 A_Raise(3);
		goto nope;
	ready:
		BFGG A 1{
			if(invoker.weaponstatus[0]&BFGF_CRITICAL)setweaponstate("shoot");
			A_WeaponReady(WRF_ALL);
		}goto readyend;
	select0:
		BFGG A 0;
		goto select0bfg;
	deselect0:
		BFGG A 0 A_JumpIf(
			invoker.weaponstatus[0]&BFGF_STRAPPED
			&&!countinv("NulledWeapon")
			,"togglestrap"
		);
		goto deselect0bfg;
	althold:
		stop;
	flash:
		BFGF B 3 bright{
			A_Light1();
			HDFlashAlpha(0,true);
		}
		BFGF A 2 bright{
			A_Light2();
			HDFlashAlpha(200);
		}
		BFGF A 2 bright HDFlashAlpha(128);
		goto lightdone;

	fire:
		BFGG C 0 {invoker.weaponstatus[BFGS_TIMER]=0;}
	hold:
		BFGG C 0{
			if(
				invoker.weaponstatus[BFGS_CHARGE]>=20    
				&& invoker.weaponstatus[BFGS_BATTERY]>=20
			)return resolvestate("chargeend");
			else if(
				invoker.weaponstatus[BFGS_CHARGE]>BFGC_MINCHARGE
				||invoker.weaponstatus[BFGS_BATTERY]>BFGC_MINCHARGE    
			)return resolvestate("charge");
			return resolvestate("nope");
		}
	charge:
		BFGG B 0{
			if(
				PressingReload()
				||invoker.weaponstatus[BFGS_BATTERY]<0
				||(
					invoker.weaponstatus[BFGS_CHARGE]>=20
					&&invoker.weaponstatus[BFGS_BATTERY]>=20    
				)
			)setweaponstate("nope");
		}
		BFGG B 6{
			invoker.weaponstatus[BFGS_TIMER]++;
			if (invoker.weaponstatus[BFGS_TIMER]>3){
				invoker.weaponstatus[BFGS_TIMER]=0;
				if(invoker.weaponstatus[BFGS_BATTERY]<20){
					invoker.weaponstatus[BFGS_BATTERY]++;
					if(invoker.weaponstatus[BFGS_BATTERY]==20)
						invoker.weaponstatus[0]|=BFGF_DEMON;
				}
				else invoker.weaponstatus[BFGS_CHARGE]++;
			}
			if(invoker.weaponstatus[BFGS_BATTERY]==20)A_SetTics(5);
			if(health<40){
				A_SetTics(4);
				if(health>16)damagemobj(invoker,self,1,"internal");    
			}
			A_TakeInventory("WeaponBusy",1);
			A_PlaySound("weapons/plasidle",5);
			BFG9k.Spark(self,1,height-10);
			A_WeaponReady(WRF_NOFIRE);
		}
		BFGG B 0{
			if(invoker.weaponstatus[BFGS_CHARGE]==20 && invoker.weaponstatus[BFGS_BATTERY]==20)
			A_Refire("shoot");
			else A_Refire();
		}
		loop;
	chargeend:
		BFGG B 2{
			BFG9k.Spark(self,1,height-10);
			A_PlaySound("weapons/plasidle",invoker.weaponstatus[BFGS_TIMER]>6?CHAN_AUTO:5);    
			A_WeaponReady(WRF_ALLOWRELOAD|WRF_NOFIRE|WRF_DISABLESWITCH);
			A_SetTics(max(1,6-invoker.weaponstatus[BFGS_TIMER]*0.3));
			invoker.weaponstatus[BFGS_TIMER]++;
		}
		BFGG B 0{
			if(invoker.weaponstatus[BFGS_TIMER]>21)A_Refire("shoot");    
			else A_Refire("chargeend");
		}goto ready;
	shoot:
		BFGG B 0{
			invoker.weaponstatus[BFGS_TIMER]=0;
			invoker.weaponstatus[0]|=BFGF_CRITICAL;
			invoker.weaponstatus[BFGS_CRITTIMER]=15;
			A_PlaySound("weapons/bfgf",CHAN_WEAPON);
			A_GiveInventory("PowerFrightener");
		}
		BFGG B 3{
			invoker.weaponstatus[BFGS_CRITTIMER]--;
			A_PlaySound("weapons/plasidle",random(5,6));
			BFG9k.Spark(self,1,height-10);
			if(invoker.weaponstatus[BFGS_CRITTIMER]<1){
				invoker.weaponstatus[BFGS_CRITTIMER]=0;
				player.setpsprite(PSP_WEAPON,invoker.findstate("reallyshoot"));
			}else if(invoker.weaponstatus[BFGS_CRITTIMER]<10)A_SetTics(2);
			else if(invoker.weaponstatus[BFGS_CRITTIMER]<5)A_SetTics(1);
		}wait;
	reallyshoot:
		BFGG A 8{
			A_AlertMonsters();
			A_GiveInventory("PowerFrightener");
		}
		BFGG B 2{
			A_PlaySound("weapons/plasmas",CHAN_AUTO);
			A_GiveInventory("PowerFrightener",1);

			invoker.weaponstatus[BFGS_CHARGE]=0;
			invoker.weaponstatus[BFGS_BATTERY]=0;
			invoker.weaponstatus[0]&=~BFGF_CRITICAL;
			A_GunFlash();
			if(random(0,7))invoker.weaponstatus[0]&=~BFGF_DEMON;
			A_SpawnItemEx("BFGBallTail",0,0,height-12,
				cos(pitch)*cos(angle)*4+vel.x,
				cos(pitch)*sin(angle)*4+vel.y,
				-sin(pitch)*4+vel.z,
				0,SXF_ABSOLUTEMOMENTUM|SXF_NOCHECKPOSITION|SXF_TRANSFERPITCH
			);
			A_SpawnItemEx("BFGBalle",0,0,height-12,
				cos(pitch)*cos(angle)*13+vel.x,
				cos(pitch)*sin(angle)*13+vel.y,
				-sin(pitch)*13+vel.z,
				0,SXF_ABSOLUTEMOMENTUM|SXF_NOCHECKPOSITION|
				SXF_TRANSFERPITCH|SXF_SETMASTER
			);
			A_GiveInventory("PowerFrightener",1);
		}
		BFGG B 0 A_JumpIf(invoker.weaponstatus[0]&BFGF_STRAPPED,"recoilstrapped");
		BFGG BC 3 A_ChangeVelocity(-2,0,3,CVF_RELATIVE);
		BFGG C 6{
			A_MuzzleClimb(
				1,3,
				-frandom(0.8,1.2),-frandom(2.4,4.6),
				-frandom(1.8,2.8),-frandom(6.4,9.6),
				1,2
			);
			if(!random(0,5))A_DropInventory("BFG9k");
		}goto nope;
	recoilstrapped:
		BFGG BBBB 1{
			A_ChangeVelocity(-0.3,0,0.06,CVF_RELATIVE);
			A_WeaponOffset(0,2,WOF_KEEPX|WOF_ADD|WOF_INTERPOLATE);
		}
		BFGG CCCC 1{
			A_WeaponOffset(0,-2,WOF_KEEPX|WOF_ADD|WOF_INTERPOLATE);
			A_MuzzleClimb(
				0.1,0.2,
				-frandom(0.08,0.1),-frandom(0.2,0.3),
				-frandom(0.18,0.24),-frandom(0.6,0.8),
				0.1,0.15
			);
		}goto nope;

	reload:
		BFGG A 0{
			if(
				invoker.weaponstatus[BFGS_BATTERY]>=20    
				||(
					!countinv("HDCellAmmo")
					&&!countinv("HDCellPacks")
				)
			){
				if(invoker.weaponstatus[BFGS_CHARGE]>BFGC_MINCHARGE)    
					setweaponstate("reloadempty");
					else setweaponstate("nope");
			}else invoker.weaponstatus[BFGS_LOADTYPE]=1;
		}goto reload1;
	altreload:
	reloadempty:
		BFGG A 0{
			if(
				!invoker.weaponstatus[BFGS_BATTERY]
				||!countinv("HDCellPackEmpty")
			)setweaponstate("nope");
			else invoker.weaponstatus[BFGS_LOADTYPE]=0;
		}goto reload1;
	unload:
		BFGG A 0{
			if(invoker.weaponstatus[BFGS_BATTERY]<0)setweaponstate("nope");
			invoker.weaponstatus[BFGS_LOADTYPE]=-1;
		}goto reload1;
	reload1:
		BFGG A 4;
		BFGG C 2 offset(0,36) A_MuzzleClimb(-frandom(1.2,2.4),frandom(1.2,2.4));
		BFGG C 2 offset(0,38) A_MuzzleClimb(-frandom(1.2,2.4),frandom(1.2,2.4));
		BFGG C 4 offset(0,40){
			A_MuzzleClimb(-frandom(1.2,2.4),frandom(1.2,2.4));
			A_PlaySound("weapons/rifleclick2");
		}
		BFGG C 2 offset(0,42){
			A_MuzzleClimb(-frandom(1.2,2.4),frandom(1.2,2.4));
			A_PlaySound("weapons/huntopen");
			if(invoker.weaponstatus[BFGS_BATTERY]>=0){    
				A_SetTics(4);
				actor a;int b;
				[b,a]=A_SpawnItemEx("HDCellPickup",
					cos(pitch)*10,0,height-10-10*sin(pitch),
					vel.x,vel.y,vel.z,
					0,SXF_ABSOLUTEMOMENTUM|SXF_NOCHECKPOSITION|SXF_TRANSFERPITCH
				);
				a.stamina=invoker.weaponstatus[BFGS_BATTERY];
			}
			if(invoker.weaponstatus[0]&BFGF_DEMON){
				setweaponstate("unloaddemon");
				invoker.weaponstatus[0]&=~BFGF_DEMON;
			}
			invoker.weaponstatus[BFGS_BATTERY]=-1;
		}goto batteryout;
	unloaddemon:
		//effects for a possessed cell
		BFGG C 0{
			if(!random(0,2))setweaponstate("vile");
			else if(!random(0,15))A_FireProjectile("YokaiSpawner");
		}goto harmless;
	vile:
		TNT1 AAAAA 0 A_FireProjectile("BFGVileShard",random(170,190),spawnofs_xy:random(-20,20));
		goto batteryout;
	harmless:
		TNT1 AAAAA 0 A_FireProjectile("BFGShard",random(170,190),spawnofs_xy:random(-20,20));
		goto batteryout;
	batteryout:
		BFGG C 4 offset(0,42){
			if(invoker.weaponstatus[BFGS_LOADTYPE]==-1)setweaponstate("reload3");
			else A_PlaySound("weapons/pocket");
		}
		BFGG C 12;
		BFGG C 12 offset(0,42)A_PlaySound("weapons/rifleload");
		BFGG C 10 offset(0,36)A_PlaySound("weapons/grenopen");
		BFGG C 0{
			if(!invoker.weaponstatus[BFGS_LOADTYPE]){
				A_TakeInventory("HDCellPackEmpty",1,TIF_NOTAKEINFINITE);
				invoker.weaponstatus[BFGS_BATTERY]=0;
			//below is just "else" since [4]-1 would never enter this state to begin with
			}else if(countinv("HDCellPacks")){
				A_TakeInventory("HDCellPacks",1,TIF_NOTAKEINFINITE);
				invoker.weaponstatus[BFGS_BATTERY]=20;
			}else if(countinv("HDCellAmmo")){
				int bt=min(countinv("HDCellAmmo"),random(1,19));
				invoker.weaponstatus[BFGS_BATTERY]=bt;
				A_TakeInventory("HDCellAmmo",bt,TIF_NOTAKEINFINITE);
			}
		}
	reload3:
		BFGG C 12 offset(0,38) A_PlaySound("weapons/huntopen");
		BFGG C 16 offset(0,37) A_PlaySound("weapons/rifleclick2");
		BFGG C 2 offset(0,38);
		BFGG C 2 offset(0,36);
		BFGG A 2 offset(0,34);
		BFGG A 12;
		goto ready;

	spawn:
		BFUG A -1 nodelay{
			if(invoker.weaponstatus[0]&BFGF_CRITICAL)invoker.setstatelabel("bwahahahaha");
		}
	bwahahahaha:
		BFUG A 3{
			invoker.weaponstatus[BFGS_CRITTIMER]--;
			A_PlaySound("weapons/plasidle",CHAN_AUTO);
			BFG9k.Spark(self,1,6);
			if(invoker.weaponstatus[BFGS_CRITTIMER]<1){
				invoker.weaponstatus[BFGS_CRITTIMER]=0;
				invoker.setstatelabel("heh");
			}else if(invoker.weaponstatus[BFGS_CRITTIMER]<10)A_SetTics(2);
			else if(invoker.weaponstatus[BFGS_CRITTIMER]<5)A_SetTics(1);
		}wait;
	heh:
		BFUG A 8;
		BFUG A 4{
			invoker.A_PlaySound("weapons/plasmas",CHAN_AUTO);
			invoker.weaponstatus[0]&=~BFGF_CRITICAL; //DO NOT DELETE THIS
			invoker.weaponstatus[BFGS_CHARGE]=0;invoker.weaponstatus[BFGS_BATTERY]=0;

			if(random(0,7))invoker.weaponstatus[0]&=~BFGF_DEMON;
			A_SpawnItemEx("BFGBallTail",0,0,height-12,
				cos(pitch)*cos(angle)*4+vel.x,
				cos(pitch)*sin(angle)*4+vel.y,
				-sin(pitch)*4+vel.z,
				0,SXF_ABSOLUTEMOMENTUM|SXF_NOCHECKPOSITION|SXF_TRANSFERPITCH
			);
			A_SpawnItemEx("BFGBalle",0,0,height-12,
				cos(pitch)*cos(angle)*13+vel.x,
				cos(pitch)*sin(angle)*13+vel.y,
				-sin(pitch)*13+vel.z,
				0,SXF_ABSOLUTEMOMENTUM|SXF_NOCHECKPOSITION|
				SXF_TRANSFERPITCH|SXF_TRANSFERPOINTERS
			);
		}
		BFUG A 0{
			invoker.A_ChangeVelocity(-cos(pitch)*4,0,sin(pitch)*4,CVF_RELATIVE);
		}goto spawn;
	}

	override void postbeginplay(){
		super.postbeginplay();
		if(owner&&owner.player&&owner.player.readyweapon==self)weaponstatus[0]|=BFGF_STRAPPED;
	}
	override void InitializeWepStats(bool idfa){
		weaponstatus[BFGS_CHARGE]=20;
		weaponstatus[BFGS_BATTERY]=20;
		weaponstatus[BFGS_TIMER]=0;
		weaponstatus[BFGS_CRITTIMER]=0;
		if(idfa){
			weaponstatus[0]&=~BFGF_DEMON;
		}else weaponstatus[0]=0;
	}
}
enum bfg9kstatus{
	BFGF_CRITICAL=1,
	BFGF_STRAPPED=2,
	BFGF_DEMON=4,

	BFGS_STATUS=0,
	BFGS_CHARGE=1,
	BFGS_BATTERY=2,
	BFGS_TIMER=3,
	BFGS_LOADTYPE=4,
	BFGS_CRITTIMER=5,

	BFGC_MINCHARGE=6,
};

class BFGSpark:HDActor{
	default{
		+nointeraction +forcexybillboard +bright
		radius 0;height 0;
		renderstyle "add";alpha 0.1; scale 0.16;
	}
	states{
	spawn:
		BFE2 DDDDDDDDDD 1 bright nodelay A_FadeIn(0.1);
		BFE2 D 1 A_FadeOut(0.3);
		wait;
	}
}
class BFGVileShard:Actor{
	default{
		+ismonster +float +nogravity +noclip +lookallaround +nofear +forcexybillboard +bright
		radius 0;height 0;
		scale 0.16;renderstyle "add";
		speed 24;
	}
	states{
	spawn:
		BFE2 A 0 nodelay{
			bfriendly=false;
			A_GiveInventory("ImmunityToFire");
			A_SetGravity(0.1);
		}
	spawn2:
		BFE2 AB 1{
			A_Look();
			A_Wander();
		}loop;
	see:
		BFE2 D 1{
			A_Wander();
			A_SpawnProjectile("BFGSpark",0,random(-24,24),random(-24,24),2,random(-14,14));
			if(!random(0,3))vel.z+=random(-4,8);
			if(alpha<0.2)setstatelabel("see2");
		}
		BFE2 A 1 bright A_Wander();
		BFE2 B 1 bright{
			A_Wander();
			A_FadeOut(0.1);
		}
		loop;
	see2:
		TNT1 AAA 0 A_Wander();
		TNT1 A 5{
			A_VileChase();
			A_SpawnItemEx("BFGSpark",random(-4,4),random(-4,4),random(28,36),random(4,6),random(-1,1),random(-6,6),random(0,360),SXF_NOCHECKPOSITION,200);
		}
		loop;
	heal:
		TNT1 A 1{
			bshootable=true;
			A_Die();
		}wait;
	death:
		BFE2 AAAAAAA 0 A_SpawnItemEx("BFGSpark",random(-4,4),random(-4,4),random(28,36),random(4,6),random(-1,1),random(-6,6),random(0,360),SXF_NOCHECKPOSITION);
		BFE2 AAAA 1 A_SpawnItemEx("BFGSpark",random(-4,4),random(-4,4),random(28,36),random(4,6),random(-1,1),random(-6,6),random(0,360),SXF_NOCHECKPOSITION);
		stop;
	}
}
class BFGShard:BFGVileShard{
	states{
	see2:
		TNT1 A 0;
		stop;
	}
}


class BFGBalle:HDFireball{
	int ballripdmg;
	default{
		+skyexplode +forceradiusdmg +ripper -noteleport +telestomp +notarget
		+bright
		decal "HDBFGLightning";
		renderstyle "add";
		damagefunction(ballripdmg);
		seesound "weapons/plasmaf";
		deathsound "weapons/bfgx";
		obituary "$OB_MPBFG_BOOM";
		alpha 0.9;
		height 6;
		radius 6;
		speed 10;
		gravity 0;
		obituary "%o was smacked by %k's big green gob.";
	}
	void A_BFGBallZap(){
		if(pos.z-floorz<12)vel.z+=1;
		else if(ceilingz-pos.z<19)vel.z-=1;

		for(int i=0;i<10;i++){
			A_SpawnParticle("green",SPF_RELATIVE|SPF_FULLBRIGHT,35,frandom(4,8),0,
				frandom(-8,8),frandom(-8,8),frandom(0,8),
				frandom(-1,1),frandom(-1,1),frandom(1,2),
				-0.1,frandom(-0.1,0.1),-0.05
			);
		}

		vector2 oldaim=(angle,pitch);
		blockthingsiterator it=blockthingsiterator.create(self,2048);
		while(it.Next()){
			actor itt=it.thing;
			if(
				(itt.bismonster||itt.player)
				&&itt!=target
				&&itt.health>0    
				&&!target.isfriend(itt)
				&&!target.isteammate(itt)
				&&checksight(itt)
			){
				A_Face(itt,0,0);
				A_CustomRailgun((0),0,"","55 ff 88",
					RGF_CENTERZ|RGF_SILENT|RGF_NOPIERCING|RGF_FULLBRIGHT,
					0,50.0,"BFGPuff",0,0,2048,18,0.2,1.0
				);
				break;
			}
		}
		angle=oldaim.x;pitch=oldaim.y;
	}
	void A_BFGBallSplodeZap(){
		blockthingsiterator it=blockthingsiterator.create(self,2048);
		while(it.Next()){
			actor itt=it.thing;
			if(
				(itt.bismonster||itt.player)
				&&itt!=target
				&&itt.health>0    
				&&!target.isfriend(itt)
				&&!target.isteammate(itt)
				&&checksight(itt)
			){
				A_Face(itt,0,0);
				int hhh=itt.health;
				for(int i=0;i<hhh;i+=1024){
					A_CustomRailgun((0),0,"","55 ff 88",
						RGF_CENTERZ|RGF_SILENT|RGF_NOPIERCING|RGF_FULLBRIGHT,
						0,50.0,"BFGPuff",3,3,2048,18,0.2,1.0
					);
				}
			}
		}
	}
	states{
	spawn:
		TNT1 A 0 nodelay{
			A_BFGSpray();
			ballripdmg=1;
			let hdp=hdplayerpawn(target);
			if(hdp&&hdp.scopecamera){
				pitch+=deltaangle(hdp.pitch,hdp.scopecamera.pitch);
				angle+=deltaangle(hdp.angle,hdp.scopecamera.angle);
			}else if(countinv("IsMoving",AAPTR_TARGET)>=6){    
				pitch+=frandom(-3,3);
				angle+=frandom(-1,1);
			}
		}
		BFS1 AB 2 A_SpawnItemEx("BFGBallTail",0,0,0,vel.x*0.2,vel.y*0.2,vel.z*0.2,0,168,0);
		BFS1 A 0{
			ballripdmg=random(500,1000);
			bripper=false;
		}
		goto spawn2;
	spawn2:
		BFS1 AB 3 A_SpawnItemEx("BFGBallTail",0,0,0,vel.x*0.2,vel.y*0.2,vel.z*0.2,0,168,0);
		---- A 0 A_BFGBallZap();
		---- A 0 A_Corkscrew();
		loop;
	death:
		BFE1 A 2;
		BFE1 B 2 A_Explode(160,512,0);
		BFE1 B 4{
			DistantQuaker.Quake(self,
				6,100,16384,10,256,512,128
			);
			A_SpawnItemEx("DistantBFG");
		}
		TNT1 AAAAA 0 A_SpawnItemEx("HDSmokeChunk",random(-2,0),random(-3,3),random(-2,2),random(-5,0),random(-5,5),random(0,5),random(100,260),SXF_TRANSFERPOINTERS|SXF_NOCHECKPOSITION,16);
		TNT1 AAAAA 0 A_SpawnItemEx("BFGBallRemains",-1,0,-12,0,0,0,SXF_TRANSFERPOINTERS|SXF_NOCHECKPOSITION,16);
		BFE1 CCCC 2 A_BFGBallSplodeZap();
		BFE1 CCC 0 A_SpawnItemEx("HDSmoke",random(-4,0),random(-3,3),random(0,4),random(-1,1),random(-1,1),random(1,3),0,SXF_TRANSFERPOINTERS|SXF_NOCHECKPOSITION,16);
		BFE1 DEF 6;
		BFE1 F 3 bright A_FadeOut(0.1);
		wait;
	}
}
class BFGBallRemains:GreenParticleFountain{
	states{
	spawn:
		TNT1 AAAAAAAAA 4 A_SpawnItemEx("HDSmoke",random(-3,3),random(-3,3),random(-3,3),random(-1,1),random(-1,1),random(1,3),0,SXF_TRANSFERPOINTERS|SXF_NOCHECKPOSITION);
		TNT1 A 8;
		stop;
	}
}
class BFGBallTail:IdleDummy{
	default{
		+forcexybillboard
		scale 0.8;renderstyle "add";
	}
	states{
	spawn:
		BFS1 AB 2 bright A_FadeOut(0.2);
		loop;
	}
}
class BFGPuff:GreenParticleFountain{
	default{
		-invisible +nointeraction +forcexybillboard +bloodlessimpact
		+noblood +alwayspuff -allowparticles +puffonactors +puffgetsowner +forceradiusdmg
		+hittracer
		renderstyle "add";
		damagetype "BFGBallAttack";
		obituary "%o couldn't be bothered to get away from %k's BFG.";
		scale 0.8;
	}
	states{
	spawn:
		BFE2 A 1 bright nodelay{
			if(target)target=target.target;
			A_PlaySound("misc/bfgrail",5);
		}
		BFE2 A 3 bright{
			A_Explode(random(196,320),320,0);

			//teleport victim
			if(
				tracer
				&&tracer!=target
				&&!tracer.player
				&&!tracer.special
				&&(
					!tracer.bismonster
					||tracer.health<1
				)
				&&!random(0,3)
			){
				spawn("TeleFog",tracer.pos);
				vector3 newpos=(angletovector(frandom(0,360),8192),0);
				tracer.setorigin(tracer.pos+newpos,false);
				spawn("TeleFog",tracer.pos);
			}
		}
		BFE2 ABCDE 2 bright A_FadeOut(0.1);
		TNT1 A 0 A_SpawnItemEx("BFGVileShard",0,0,10,10,0,0,random(0,360),SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS,254);
		stop;
	}
}


//until we scriptify the bossbrain properly...
class BFGAccelerator:IdleDummy{
	states{
	spawn:
		TNT1 A 0 nodelay{
			thinkeriterator it=thinkeriterator.create("BFG9k");
			actor bff;
			while(
				bff=inventory(it.Next())
			){
				let bfff=BFG9k(Bff);
				if(bfff){
					bfff.weaponstatus[randompick(BFGS_BATTERY,BFGS_CHARGE)]++;
				}
			}
		}stop;
	}
}

