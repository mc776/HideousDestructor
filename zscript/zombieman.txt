// ------------------------------------------------------------
// Former Human
// ------------------------------------------------------------
class ZombieStormtrooper:ZombieMan{
	default{
		translation "58:66=128:136","214:223=141:148","176:191=24:47","16:34=68:79";
		health 70; gibhealth 100;
		height 52;radius 12;
		speed 8;
		dropitem "";attacksound "";decal "BulletScratch";
		painchance 250;
		obituary "%o was gunned down by a zombieman.";
		hitobituary "%o was beaten up by a zombieman.";
		accuracy 0;
	}
	double spread;
	double turnamount;
	int mag;
	int firemode; //-2 SMG; -1 semi only; 0 semi; 1 auto; 2+ burst
	bool hasdropped;
	override void postbeginplay(){
		super.postbeginplay();
		if(accuracy==1)firemode=random(0,5);
		else if(accuracy==2)firemode=-1;
		else if(accuracy==3)firemode=-2;
		else firemode=random(-2,4);
		if(firemode==-2){
			mag=30;
			A_SetTranslation("ZombieSMG");
			maxtargetrange=6000;
		}else{
			mag=50;
			if(firemode>2)firemode=1;
		}
		hdmobster.spawnmobster(self);
	}
	void noblockwepdrop(){
		A_NoBlocking();
		hdweapon wp;
		if(firemode==-2){
			if(!hasdropped){
				if(wp=hdweapon(spawn("HDSMG",pos))){
					wp.weaponstatus[SMGS_AUTO]=2;
					wp.weaponstatus[SMGS_MAG]=random(0,30);
					wp.weaponstatus[SMGS_CHAMBER]=2;
				}
				A_DropItem("HDFragGrenadeAmmo",0,8);
				A_DropItem("HDFragGrenadeAmmo",0,4);
				A_DropItem("HDSMGMag");
			}else{
				A_DropItem("HDSMGMag",random(1,2));
				A_DropItem("HDFragGrenadeAmmo",0,4);
				A_DropItem("HDFragGrenadeAmmo",0,4);
			}
		}else if(firemode==-1){
			if(!hasdropped){
				if(wp=hdweapon(spawn("ZM66AssaultRifle",pos))){
					wp.weaponstatus[0]=
					ZM66F_NOFIRESELECT|ZM66F_NOLAUNCHER|(random(0,1)*ZM66F_CHAMBER);
					wp.weaponstatus[ZM66S_MAG]=random(0,50);
					if(wp.weaponstatus[ZM66S_MAG]==50)wp.weaponstatus[ZM66S_MAG]=51;
					wp.weaponstatus[ZM66S_ZOOM]=random(0,27);
				}
			}else{
				A_DropItem("ZM66RifleMags",0,96);
			}
		}else{
			if(!hasdropped){
				if(wp=hdweapon(spawn("ZM66AssaultRifle",pos))){
					wp.weaponstatus[0]=
					ZM66F_NOLAUNCHER|(random(0,1)*ZM66F_CHAMBER);
					wp.weaponstatus[ZM66S_MAG]=random(0,50);
					wp.weaponstatus[ZM66S_AUTO]=random(0,1);
					wp.weaponstatus[ZM66S_ZOOM]=random(0,27);
				}
			}else{
				A_DropItem("ZM66RifleMags",0,96);
			}
		}
		if(wp){
			wp.bdropped=true;
			wp.addz(40);
			wp.vel=vel+(frandom(-2,2),frandom(-2,2),1);
		}
		if(!hasdropped){
			A_DropItem("HDPistol",0,16);
			hasdropped=true;
		}
	}
	states{
	spawn:
		POSS A 0;
	spawn2:
		#### A 0{
			A_Look();
			A_Recoil(frandom(-0.1,0.1));
		}
		#### EEE 1{
			A_SetTics(random(5,17));
			A_Look();
		}
		#### E 1{
			A_Recoil(frandom(-0.1,0.1));
			A_SetTics(random(10,40));
		}
		#### B 0 A_Jump(28,"spawngrunt");
		#### B 0 A_Jump(132,"spawnswitch");
		#### B 8 A_Recoil(frandom(-0.2,0.2));
		loop;
	spawngrunt:
		#### G 1{
			A_Recoil(frandom(-0.4,0.4));
			A_SetTics(random(30,80));
			A_PlaySound("grunt/active");
		}
		#### A 0 A_Jump(256,"spawn2");
	spawnswitch:
		#### A 0 A_JumpIf(bambush,"spawnstill");
		goto spawnwander;
	spawnstill:
		#### A 0 A_Look();
		#### A 0 A_Recoil(random(-1,1)*0.4);
		#### CD 5 A_SetAngle(angle+random(-4,4));
		#### A 0{
			A_Look();
			if(!random(0,127))A_PlaySound("grunt/active");
		}
		#### AB 5 A_SetAngle(angle+random(-4,4));
		#### B 1 A_SetTics(random(10,40));
		#### A 0 A_Jump(256,"spawn2");
	spawnwander:
		#### CDAB 5{hdmobai.wander(self,false);}
		#### A 0{
			if(!random(0,127))A_PlaySound("grunt/active");
		}
		#### A 0 A_Jump(64,"spawn2");
		loop;
	missile:
		#### A 0{
			double dt=distance3d(target);
			if(
				firemode==-2
				&&target
				&&!random(0,39)
				&&dt>200
				&&dt<1000
			)setstatelabel("frag");
		}
		#### A 0 A_JumpIf(mag<1,"reload");
		#### A 0 A_JumpIfTargetInLOS(3,120);
		#### CD 2 A_FaceTarget(90);
		#### E 1 A_SetTics(random(4,10)); //when they just start to aim,not for followup shots!
		#### A 0 A_JumpIfTargetInLOS("missile2");
		#### A 0 A_CheckLOF("see",
			CLOFF_JUMPNONHOSTILE|CLOFF_SKIPTARGET|
			CLOFF_JUMPOBJECT|CLOFF_MUSTBESOLID|
			CLOFF_SKIPENEMY,
			0,0,0,0,44,0
		);
	missile2:
		#### A 0{
			if(!target){
				setstatelabel("spawn2");
				return;
			}
			double enemydist=distance3d(target);
			if(enemydist<200)turnamount=50;
			else if(enemydist<600)turnamount=30;
			else turnamount=10;
		}goto turntoaim;
	turntoaim:
		#### E 2 A_FaceTarget(turnamount,turnamount);
		#### A 0 A_JumpIfTargetInLOS(1);
		goto see;
		#### A 0 A_JumpIfTargetInLOS(1,10);
		loop;
		#### E 1{
			A_FaceTarget(turnamount,turnamount);
			A_SetTics(random(0,100/clamp(turnamount,1,turnamount+1)+4));
			spread=frandom(0.1,0.2)*turnamount;
		}
		#### A 0 A_Jump(256,"shoot");
	shoot:
		#### F 1 bright light("SHOT"){
			if(mag<1){
				setstatelabel("ohforfuckssake");
				return;
			}
			actor p;
			if(firemode==-2){
				pitch+=frandom(0,spread)-frandom(0,spread);
				angle+=frandom(0,spread)-frandom(0,spread);
				A_PlaySound("weapons/pistol",CHAN_WEAPON);
				p=spawn("HDBullet9",pos+(0,0,height-6));
				p.speed=500+10*frandom(-1.,1.);
				A_SpawnItemEx("HDSpent9mm",
					cos(pitch)*10,0,height-8-sin(pitch)*10,
					vel.x,vel.y,vel.z,
					0,SXF_ABSOLUTEMOMENTUM|SXF_NOCHECKPOSITION|SXF_TRANSFERPITCH
				);
			}else{
				pitch+=frandom(-spread,spread);
				angle+=frandom(-spread,spread);
				A_PlaySound("weapons/rifle",CHAN_WEAPON);
				p=spawn("HDBullet426",pos+(0,0,height-6));
			}
			p.target=self;p.angle=angle;p.pitch=pitch;
			p.vel+=self.vel;

			mag--;
		}
		#### E 2{
			if(
				//conditions under which no autofire can happen
				!firemode
				||firemode==-1
				||firemode>3
				||mag<1
			){
				if(firemode>2)firemode=2;
				setstatelabel("postshot");
			}else if(
				//if burst, do not wait the tics of this frame
				firemode>=2
			){
				firemode++;
				setstatelabel("shoot");
			}
			else spread++;
		}
		#### A 0 A_Jump(120,"shoot");
		//fallthrough to postshot
	postshot:
		#### E 5{
			if(!random(0,127))A_PlaySound("grunt/active");
			if(mag<1){
				setstatelabel("reload");
				return;
			}
			spread=max(0,spread-1);
			A_SetTics(random(2,6));
		}
		#### E 3;
		#### E 0 A_JumpIfTargetInLOS(1);
		goto coverfire;//even if not in los,occasionally keep shooting anyway
		#### E 3 A_FaceTarget(10,10);
		#### E 0 A_Jump(30,"see");//even if in los,occasionally stop shooting anyway
		goto coverfire;

	coverfire:
		#### E 1 A_SetTics(random(2,12));
		#### E 0{
			spread=2;
		}
		#### E 0 A_Jump(90,"roam");
		#### E 0 A_JumpIfTargetInLOS("missile2");
		#### E 0 A_Jump(216,"shoot");
		loop;

	frag:
		---- A 10 A_PlaySound("grunt/sight",CHAN_VOICE);
		---- A 20{
			A_PlaySound("weapons/pocket",CHAN_WEAPON);
			A_FaceTarget(0,0);
			pitch-=random(10,50);
		}
		---- A 10{
			A_SpawnItemEx("HDFragSpoon",cos(pitch)*4,-1,height-6-sin(pitch)*4,cos(pitch)*cos(angle)*4+vel.x,cos(pitch)*sin(angle)*4+vel.y,sin(-pitch)*4+vel.z,0,SXF_ABSOLUTEMOMENTUM|SXF_NOCHECKPOSITION|SXF_TRANSFERPITCH);
			A_GiveInventory("GrenadeForce",random(10,30));
			A_GiveInventory("HDGrenadeToss");
		}
		---- A 0 A_JumpIf(mag<1,"reload");
		goto see;

	ohforfuckssake:
		#### E 8;
	reload:
		---- A 4{
			A_PlaySound("weapons/rifleclick2");
			bfrightened=true;
		}
		#### AA 1{hdmobai.chase(self,"melee",null);}
		#### A 0{
			A_PlaySound("weapons/rifleload");
			if(firemode==-2)A_SpawnProjectile("HDSMGEmptyMag",38,0,random(90,120));
			else A_SpawnProjectile("HD4mmMagEmpty",38,0,random(90,120));
		}
		#### BCD 2 {hdmobai.chase(self,"melee",null);}
		#### E 12 A_PlaySound("weapons/pocket");
		#### E 8 A_PlaySound("weapons/rifleload");
		#### E 2{
			A_PlaySound("weapons/rifleclick2");
			if(firemode==-2)mag=30;else mag=50;
			bfrightened=false;
		}
		#### CCBB 2{hdmobai.wander(self,true);}

	see:
		#### A 0{
			if(firemode>=0)firemode=random(0,2);
			if(health<20){
				bnopain=true;
				damagemobj(self,target,1,"bleedout");
				bnopain=false;
			}
		}
	see2:
		#### A 0{
			if(mag<1)setstatelabel("reload");
		}
		#### AABBCCDD 2 {hdmobai.chase(self);}
		#### A 0{
			spread=2;
		}
		#### A 0 A_JumpIfTargetInLOS("see");
		#### A 0 A_Jump(24,"roam");
		loop;
	roam:
		#### E 3 A_Jump(60,"roam2");
		#### E 0{spread=1;}
		#### EEEE 1 A_Chase("melee","turnaround",CHF_DONTMOVE);
		#### E 0{spread=0;}
		#### EEEEEEEEEEEEE 1 A_Chase("melee","turnaround",CHF_DONTMOVE);
		#### A 0 A_Jump(60,"roam");
	roam2:
		#### A 0 A_Jump(8,"see");
		#### A 0 HealThing(random(0,2));
		#### A 5{hdmobai.chase(self);}
		#### BC 5{hdmobai.wander(self,true);}
		#### D 5{hdmobai.chase(self);}
		#### A 0 A_Jump(140,"Roam");
		#### A 0 A_AlertMonsters();
		#### A 0 A_JumpIfTargetInLOS("see");
		loop;
	turnaround:
		#### A 0 A_FaceTarget(15,0);
		#### E 2 A_JumpIfTargetInLOS("missile2",40);
		#### E 0{spread=3;}
		#### A 0 A_FaceTarget(15,0);
		#### E 0{spread=6;}
		#### E 2 A_JumpIfTargetInLOS("missile2",40);
		#### E 0{spread=4;}
		#### ABCD 3{hdmobai.chase(self);}
		goto see;
	melee:
		#### C 8 A_FaceTarget();
		#### D 4;
		#### E 4 A_CustomMeleeAttack(
			random(3,20),"weapons/smack","","none",randompick(0,0,0,1)
		);
		#### E 3 A_JumpIfCloser(64,2);
		#### E 4 A_FaceTarget();
		goto missile2;
		#### A 4;
		goto see;
	pain:
		#### G 2;
		#### G 3{
			A_Pain();
			if(!random(0,10))A_AlertMonsters();
		}
		#### G 0{
			if(target&&distance3d(target)<100)setstatelabel("see");
			else bfrightened=true;
		}
		#### ABCD 2{hdmobai.chase(self);}
		#### G 0{bfrightened=false;}
		goto see;
	death.bleedout:
		#### H 5{
			{hdmobai.corpseflags(self);}
			actor ttt=spawn("tempshield",pos);
			ttt.vel=vel;ttt.master=self;
		}
		#### I 5{
			if(!random(0,2))A_PlaySound("grunt/bleedout",CHAN_VOICE);
		}
		goto deathpostscream;
	death:
		#### H 5{
			{hdmobai.corpseflags(self);}
			actor ttt=spawn("tempshield",pos);
			ttt.vel=vel;ttt.master=self;
		}
		#### I 5 A_Scream();
	deathpostscream:
		#### J 5{
			actor ttt=spawn("tempshield2",pos);
			ttt.vel=vel;ttt.master=self;
			noblockwepdrop();
		}
		#### K 5;
	dead:
		#### K 3 canraise{if(abs(vel.z)<2.)frame++;}
		#### L 5 canraise{if(abs(vel.z)>=2.)setstatelabel("dead");}
		wait;
	xdeathbrewtlelulz:
		#### M 5{
			bshootable=false;
			A_GiveInventory("IsGibbed");
		}
		#### N 5{
			spawn("MegaBloodSplatter",pos+(0,0,34));
			A_XScream();
		}
		#### OPQRST 5;
		goto xdead;
	xdeath:
		#### M 5{
			bshootable=false;
			A_GiveInventory("IsGibbed");
		}
		#### N 5{
			spawn("MegaBloodSplatter",pos+(0,0,34));
			A_XScream();
		}
		#### O 0 noblockwepdrop();
		#### OP 5 spawn("MegaBloodSplatter",pos+(0,0,34));
		#### QRST 5;
		goto xdead;
	xdead:
		#### T 3 canraise{if(abs(vel.z)<2.)frame++;}
		#### U 5 canraise{if(abs(vel.z)>=2.)setstatelabel("xdead");}
		wait;
	raise:
		#### A 0{hdmobai.corpseflags(self);}
		#### A 0 A_JumpIfInventory("IsGibbed",1,"RaiseGibbed");
		#### L 4 spawn("MegaBloodSplatter",pos+(0,0,34));
		#### LK 6;
		#### JIH 4;
		goto see;
	raisegibbed:
		#### U 12{
			A_TakeInventory("IsGibbed");
			spawn("MegaBloodSplatter",pos+(0,0,34));
		}
		#### T 8;
		#### SRQ 6;
		#### PONM 4;
		#### L 0 A_Die("Ungibbed");
	death.ungibbed:
		#### H 5{
			{hdmobai.corpseflags(self);}
			actor ttt=spawn("tempshield",pos);
			ttt.vel=vel;ttt.master=self;
		}
		#### I 5;
		#### J 5{
			actor ttt=spawn("tempshield2",pos);
			ttt.vel=vel;ttt.master=self;
			A_NoBlocking();
		}
		#### K 5;
		goto dead;
	}
}


class ZombieAutoStormtrooper:ZombieStormTrooper{default{accuracy 1;}}
class ZombieSemiStormtrooper:ZombieStormTrooper{default{accuracy 2;}}
class ZombieSMGStormtrooper:ZombieStormTrooper{default{accuracy 3;}}

class ZombieHideousTrooper:RandomSpawner replaces ZombieMan{
	default{
		dropitem "ZombieStormtrooper",256,100;
		dropitem "EnemyHERP",256,1;
	}
}
class DeadZombieStormtrooper:ZombieStormtrooper replaces DeadZombieMan{
	override void postbeginplay(){
		super.postbeginplay();
		A_GiveInventory("HasDroppedGun");
		A_Die("spawndead");
	}
	states{
	death.spawndead:
		POSS A 0{
			hdmobai.CorpseFlags(self);
			bnodropoff=false;
		}
		goto dead;
	}
}
class DeadZombieAutoStormtrooper:DeadZombieStormtrooper{default{accuracy 1;}}
class DeadZombieSemiStormtrooper:DeadZombieStormtrooper{default{accuracy 2;}}
class DeadZombieSMGStormtrooper:DeadZombieStormtrooper{default{accuracy 3;}}



