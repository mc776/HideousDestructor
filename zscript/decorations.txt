// ------------------------------------------------------------
// Props, trees, headsicles, etc.
// ------------------------------------------------------------
class HDFloatingSkull:HDActor replaces FloatingSkull{
	default{
		height 47;radius 16;+nogravity;+noteleport;+solid;
		+shootable;+nodamage;+noblooddecals;
		+pushable;pushfactor 0.3;mass 300;
		bloodtype "HDSmokeChunk";
	}
	states{
	spawn:
		FSKU AB random(2,8) bright light("FSKUL"){
			double ud=frandom(-0.05,0.05);
			if(pos.z-floorz<4){
				ud=0.05;
			}
			else if(pos.z-floorz>28){
				ud=-0.05;
			}
			vel+=(frandom(-0.05,0.05),frandom(-0.05,0.05),ud);
		}loop;
	}
}


class HDTree:HDActor{
	//seeing if you're standing on a hellish texture
	static const string hlf[]={
		"SFLR6_1","SFLR6_4","SFLR7_1","SFLR7_4",
		"BLOOD1","BLOOD2","BLOOD3",
		"LAVA1","LAVA2","LAVA3","LAVA4"
	};
	bool CheckHellFloor(){
		int hlflength=hlf.size();
		for (int i=0;i<hlflength;i++){
			TextureID tx=TexMan.CheckForTexture(hlf[i],TexMan.Type_Flat);
			if (tx==floorpic){
				return true;
			}
		}
		return false;
	}
	default{
		+solid +shootable +nodamage +dontthrust +forceybillboard +rollsprite
		mass int.MAX;painchance 48;
	}
	void A_Resize(double scx,double scy){
		super.postbeginplay();
		double cz=ceilingz-floorz;
		if(cz>0&&cz<height*scy)scy*=height/cz;
		A_SetSize(radius*scx,height*scy);
		scale=(scale.x*scx,scale.y*scy);
		scale.x*=randompick(-1,1);
		roll+=frandom(-5.,5.);
	}
	states{
	quiet:
		---- A -1{
			bnoblood=true;
			bnopain=true;
		}stop;
	spawn2:
		---- A 1 A_CheckFloor(1);
		wait;
		---- A 0{
			if(
				CheckHellFloor()
			)setstatelabel("spawn3");
			angle=frandom(0,360);
		}goto quiet;
	spawn3:
		---- A 1{
			A_SetTics(random(1,20)*10);
			if(random(0,7))A_PlaySound("grunt/active",random(0,7),frandom(0.1,0.4),0,1);
			else A_PlaySound("tree/pain",random(0,7),frandom(0.2,1.0),0,1);
		}loop;
	pain:
		---- A 1{
			for(int i=0;i<7;i++){
				A_StopSound(i);
			}
			A_PlaySound("tree/pain",0,1.0,0,1);
			A_Immolate(self,self,random(1,10)*8);
			bnopain=true;
			A_SetTics(random(1,10)*40);
		}
		---- A 0{bnopain=false;}
		goto spawn2;
	}
}




class HDElectricLampLight:PointLight{
	override void postbeginplay(){
		super.postbeginplay();
		if(!target){destroy();return;}
		args[0]=140;
		args[1]=164;
		args[2]=196;
		args[3]=32+target.height;
		args[4]=0;
	}
	override void Tick(){
		if(!target){destroy();return;}
		setorigin((target.pos.xy,target.pos.z+target.height-7),true);
	}
}
class HDTechPillar:TechPillar replaces TechPillar{
	default{+dontthrust +shootable +nodamage +noblood +forceybillboard mass 2000;}
}
class HDColumn:Column replaces Column{
	default{+dontthrust +shootable +nodamage +noblood +forceybillboard radius 6;
		height 42;
	}
	override void postbeginplay(){
		super.postbeginplay();
		A_SpawnItemEx("HDElectricLampLight",SXF_SETTARGET);
	}
}
class HDTechLamp:TechLamp replaces TechLamp{
	default{+dontthrust +shootable +nodamage +noblood +forceybillboard radius 6;
		height 69;
	}
	override void postbeginplay(){
		super.postbeginplay();
		A_SpawnItemEx("HDElectricLampLight",SXF_SETTARGET);
	}
}
class HDTechLamp_:HDActor replaces TechLamp{ //used in the range, so gotta check
	default{+dontthrust +shootable +nodamage +noblood +forceybillboard radius 6;}
	override void postbeginplay(){
		super.postbeginplay();
		if(texman.checkfortexture("TLMPA0",texman.type_sprite).isvalid())spawn("HDTechLamp",pos,ALLOW_REPLACE);
		else spawn("HDColumn",pos,ALLOW_REPLACE);
		destroy();
	}
}
class HDTechLamp2:TechLamp2 replaces TechLamp2{
	default{+dontthrust +shootable +nodamage +noblood +forceybillboard radius 6;}
	override void postbeginplay(){
		super.postbeginplay();
		A_SpawnItemEx("HDElectricLampLight",SXF_SETTARGET);
	}
}



