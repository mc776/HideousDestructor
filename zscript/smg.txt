// ------------------------------------------------------------
// SMG
// ------------------------------------------------------------
class HDSMG:HDWeapon{
	default{
		obituary "%o was soaked by %k's pea stream.";
		weapon.selectionorder 40;
		weapon.slotnumber 2;
		weapon.kickback 30;
		weapon.bobrangex 0.3;
		weapon.bobrangey 0.6;
		weapon.bobspeed 2.5;
		scale 0.55;
		inventory.pickupmessage "You got the SMG!";
	}
	override void postbeginplay(){
		super.postbeginplay();
		if(owner&&owner.player)weaponstatus[SMGS_AUTO]=clamp(
			cvar.getcvar("hd_smgfiremode",owner.player).getint(),
			0,2
		);
	}
	states{
	select0:
		SMGG A 0;
		goto select0small;
	deselect0:
		SMGG A 0;
		goto deselect0small;

	ready:
		SMGG A 1{
			A_SetCrosshair(21);
			invoker.weaponstatus[SMGS_RATCHET]=0;
			A_WeaponReady(WRF_ALL);
			A_GiveInventory("NotShot",1);
			if(
				(countinv("NotShot")>469)	//timer
				&&(countinv("HDPistolAmmo")>29)	//have enough loose  
				&&(!A_JumpIfInventory("HDSMGMag",0,"null"))	//not maxed out on mags
			){
				A_GiveInventory("HDSMGMag",1);
				A_TakeInventory("HDPistolAmmo",30);
				A_TakeInventory("NotShot");
			}
		}
		goto readyend;
	altfire:
	althold:
		goto nope;
	hold:
		SMGG A 0{
			if(
				//full auto
				invoker.weaponstatus[SMGS_AUTO]==2
			)setweaponstate("fire2");
			else if(
				//burst
				invoker.weaponstatus[SMGS_AUTO]<1
				||invoker.weaponstatus[SMGS_RATCHET]>2
			)setweaponstate("nope");
		}goto fire;
	user2:
	firemode:
		---- A 1{
			int aut=invoker.weaponstatus[SMGS_AUTO];
			if(aut>=2)invoker.weaponstatus[SMGS_AUTO]=0;
			else if(aut<0)invoker.weaponstatus[SMGS_AUTO]=0;
			else invoker.weaponstatus[SMGS_AUTO]++;
		}goto nope;
	fire:
		SMGG A 1;
	fire2:
		SMGG B 1{
			if(invoker.weaponstatus[SMGS_CHAMBER]==2){
				A_GunFlash();
			}else{
				if(invoker.weaponstatus[SMGS_MAG]>0)setweaponstate("chamber");
				else setweaponstate("nope");
			}
		}
		SMGG A 1;
		SMGG A 0{
			A_SpawnItemEx("HDSpent9mm",
				cos(pitch)*10,0,height-8-sin(pitch)*10,
				vel.x,vel.y,vel.z,
				0,SXF_ABSOLUTEMOMENTUM|SXF_NOCHECKPOSITION|SXF_TRANSFERPITCH
			);
			if(invoker.weaponstatus[SMGS_AUTO]==2)A_SetTics(1);
		}
		SMGG A 0 A_ReFire();
		goto ready;
	flash:
		SMGG B 0{
			actor p=spawn("HDBullet9",pos+(0,0,height-6));
			p.target=self;p.angle=angle;p.pitch=pitch;
			p.speed=500+10*frandom(-1.,1.);
			p.vel+=(
				frandom(-1.,1.),frandom(-1.,1.),frandom(-1.,1.)
			);
			p.vel+=self.vel;

			A_ZoomFactor(0.995,ZOOM_INSTANT|ZOOM_NOSCALETURNING);
			A_PlaySound("weapons/pistol",CHAN_WEAPON,0.7);
			invoker.weaponstatus[SMGS_RATCHET]++;
			invoker.weaponstatus[SMGS_CHAMBER]=1;
		}
		SMGF A 1 bright{
			HDFlashAlpha(-200);
			A_Light1();
		}
		TNT1 A 0{
			A_Light0();
			A_ZoomFactor(1.0,ZOOM_INSTANT|ZOOM_NOSCALETURNING);
			if(invoker.weaponstatus[SMGS_MAG]>0){
				invoker.weaponstatus[SMGS_MAG]--;
				invoker.weaponstatus[SMGS_CHAMBER]=2;
			}
		}
		TNT1 AA 1 A_MuzzleClimb(-frandom(0.4,0.6),-frandom(0.5,0.8));
		stop;


	unloadchamber:
		SMGG B 4 A_JumpIf(invoker.weaponstatus[SMGS_CHAMBER]<1,"nope");
		SMGG B 10{
			invoker.weaponstatus[SMGS_CHAMBER]=0;
			A_SpawnItemEx("HDPistolAmmo",
				cos(pitch)*10,0,height-8-sin(pitch)*10,
				vel.x,vel.y,vel.z,
				0,SXF_ABSOLUTEMOMENTUM|SXF_NOCHECKPOSITION|SXF_TRANSFERPITCH
			);
		}goto readyend;
	user4:
	unload:
		SMGG A 0{
			invoker.weaponstatus[0]|=SMGF_JUSTUNLOAD;
			if(
				invoker.weaponstatus[SMGS_MAG]>=0
			)setweaponstate("unmag");
			else if(invoker.weaponstatus[SMGS_CHAMBER]>0)setweaponstate("unloadchamber");
		}goto nope;
	reload:
		SMGG A 0{
			invoker.weaponstatus[0]&=~SMGF_JUSTUNLOAD;
			if(
				invoker.weaponstatus[SMGS_MAG]>=30
				||(!countinv("HDSMGMag")&&!countinv("HDPistolAmmo"))
			)setweaponstate("nope");
		}goto unmag;
	unmag:
		SMGG A 1 offset(0,34) A_SetCrosshair(21);
		SMGG A 1 offset(5,38);
		SMGG A 1 offset(10,42);
		SMGG B 2 offset(20,46) A_PlaySound("weapons/rifleclick",CHAN_WEAPON);
		SMGG B 4 offset(30,52){
			A_MuzzleClimb(0.3,0.4);
			A_PlaySound("weapons/rifleload",CHAN_WEAPON);
		}
		SMGG B 0{
			if(invoker.weaponstatus[SMGS_MAG]<0){
				setweaponstate("magout");
				return;
			}
			if(
				!invoker.weaponstatus[SMGS_MAG]
				||(!countinv("PressingUnload")&&!countinv("PressingReload"))
				||(
					countinv("HDPistolAmmo")+invoker.weaponstatus[SMGS_MAG]
					>AmmoCap("HDPistolAmmo")
				)
				||(
					invoker.weaponstatus[SMGS_MAG]>=30
					&&A_JumpIfInventory("HDSMGMag",0,"null")
				)
			){
				actor aaa;int bbb;
				[bbb,aaa]=A_SpawnItemEx("HD9mSmag",12,0,height-12,1,0,-1,0,SXF_SETTARGET);
				aaa.stamina=invoker.weaponstatus[SMGS_MAG];
				invoker.weaponstatus[SMGS_MAG]=-1;
				setweaponstate("magout");
			}
		}
	pocketmag:
		SMGG B 0{
			A_PlaySound("weapons/pocket");
			int sl=invoker.weaponstatus[SMGS_MAG];
			if(sl>=30)A_GiveInventory("HDSMGMag",1);
			else A_GiveInventory("HDPistolAmmo",sl);
			invoker.weaponstatus[SMGS_MAG]=-1;
		}
		SMGG BB 7 offset(34,54) A_MuzzleClimb(frandom(0.2,-0.8),frandom(-0.2,0.4));
	magout:
		SMGG B 0{
			if(invoker.weaponstatus[0]&SMGF_JUSTUNLOAD)setweaponstate("reloadend");
			else setweaponstate("loadmag");
		}

	loadmag:
		SMGG B 0 A_PlaySound("weapons/pocket",CHAN_WEAPON);
		SMGG BB 7 offset(34,54) A_MuzzleClimb(frandom(0.2,-0.8),frandom(-0.2,0.4));
		SMGG B 8 offset(34,52) A_PlaySound("weapons/pocket",CHAN_WEAPON);
		SMGG B 12 offset(32,50);
		SMGG B 3 offset(32,49) A_PlaySound("weapons/rifleclick",CHAN_WEAPON);
		SMGG B 0{
			if(countinv("HDSMGMag")){
				invoker.weaponstatus[SMGS_MAG]=30;
				A_TakeInventory("HDSMGMag",1,TIF_NOTAKEINFINITE);
			}else{
				int rnd=min(random(1,30),countinv("HDPistolAmmo"));
				invoker.weaponstatus[SMGS_MAG]=rnd;
				A_TakeInventory("HDPistolAmmo",rnd,TIF_NOTAKEINFINITE);
			}
			if(invoker.weaponstatus[SMGS_CHAMBER]==2)setweaponstate("reloadend");
		}
	chamber:
		SMGG B 2 offset(33,50){
			invoker.weaponstatus[SMGS_MAG]--;
			invoker.weaponstatus[SMGS_CHAMBER]=2;
		}
		SMGG B 4 offset(32,49) A_PlaySound("weapons/rifleclick2",CHAN_WEAPON);

	reloadend:
		SMGG B 3 offset(30,52);
		SMGG B 2 offset(20,46);
		SMGG A 1 offset(10,42);
		SMGG A 1 offset(5,38);
		SMGG A 1 offset(0,34);
		goto nope;


	zoom:
	user3:
	cannibalize:
		SMGG A 0 A_SetCrosshair(21);
		SMGG A 0 A_JumpIf(!countinv("HDPistolMag"),"nope");
		SMGG B 4 offset(0,34);
		SMGG B 2 offset(0,44);
		SMGG B 2 offset(0,70);

		SMGG AA 4 A_PlaySound("weapons/pocket",CHAN_WEAPON);
		SMGG A 10 A_MuzzleClimb(frandom(0.2,-0.8),frandom(-0.2,0.4));
		SMGG A 3 A_PlaySound("weapons/rifleclick2",CHAN_WEAPON);
		SMGG AA 2 A_PlaySound("weapons/rifleclick2",CHAN_WEAPON);
		SMGG A 0 A_MuzzleClimb(frandom(0.2,-0.8),frandom(-0.2,0.4));
		SMGG AAAAAAAA 1 A_PlaySound("weapons/rifleclick2",CHAN_WEAPON);
		SMGG AA 2 A_PlaySound("weapons/rifleclick2",CHAN_WEAPON);
		SMGG A 0 A_MuzzleClimb(frandom(0.2,-0.8),frandom(-0.2,0.4));
		SMGG AA 3 A_PlaySound("weapons/rifleclick2",CHAN_WEAPON);
		SMGG A 0 A_PlaySound("weapons/rifleclick",CHAN_WEAPON);
		SMGG A 0 A_MuzzleClimb(frandom(0.2,-0.8),frandom(-0.2,0.4));
		SMGG AAAAAAAAAAAAAAA 1;
		SMGG A 12{
			A_PlaySound("weapons/pocket",CHAN_WEAPON);
			A_TakeInventory("HDPistolMag",1);
			int room=ammocap("HDPistolAmmo")-countinv("HDPistolAmmo");
			A_GiveInventory("HDPistolAmmo",room);
			if(room<15){
				room=15-room;
				while(room>0){
					room--;
					A_SpawnItemEx("HDPistolAmmo",
						random(8,12),random(0,-12),24,
						random(3,6),0,0,random(6,12)
					);
				}
			}
		}
		SMGG B 2 offset(0,70);
		SMGG B 2 offset(0,44);
		SMGG B 4 offset(0,34);
		goto nope;
	spawn:
		SMGN A -1;
	}
	override void initializewepstats(bool idfa=false){
		weaponstatus[0]=0;
		weaponstatus[SMGS_MAG]=30;
		weaponstatus[SMGS_CHAMBER]=2;
		if(!idfa)weaponstatus[SMGS_AUTO]=0;
	}
}
enum smgstatus{
	SMGF_JUSTUNLOAD=1,

	SMGS_FLAGS=0,
	SMGS_MAG=1,
	SMGS_CHAMBER=2, //0 empty, 1 spent, 2 loaded
	SMGS_AUTO=3, //0 semi, 1 burst, 2 auto
	SMGS_RATCHET=4,
};
class HDSMGEmptyMag:HDPistolEmptyMag{
	states{
	spawn:
	death:
		CLP3 B -1;
		stop;
	}
}
