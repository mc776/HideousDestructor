// ------------------------------------------------------------
// Baron Of Hell
// ------------------------------------------------------------
class HellPrince:BaronOfHell replaces BaronofHell{
	default{
		+missilemore +seeinvisible +dontharmspecies
		maxtargetrange 65536;
		bloodcolor "44 99 22";
		damagefactor "thermal",0.8;
		damagefactor "smallarms0",0.86;
		damagefactor "smallarms1",0.95;
		damagefactor "balefire",0.3;
		meleedamage 12;
		meleerange 58;
		health BE_HPMAX;
		meleesound "baron/melee";
		speed 6;
		painchance 4;
		obituary "%o was sauteed by a baron of hell.";
		hitobituary "%o was shredded by a baron of hell.";
	}
	void A_BaronSoul(){
			let aaa=Condemned(spawn("LostCondemned",pos));
			aaa.addz(32);
			aaa.master=self;
			aaa.target=target;
			aaa.angle=angle;
			aaa.pitch=pitch;
			aaa.spitter=self;
			aaa.vel=vel;
			aaa.A_SkullLaunch();
			shields-=66;
	}
	int shields;
	enum BaronStats{
		BE_SHIELDMAX=66,
		BE_HPMAX=1000,
		BE_OKAY=BE_HPMAX*7/10,
		BE_BAD=BE_HPMAX*3/10,
	}
	override int damagemobj(
		actor inflictor,actor source,int damage,
		name mod,int flags,double angle
	){
		if(damage==TELEFRAG_DAMAGE)return super.damagemobj(
			inflictor,source,TELEFRAG_DAMAGE,
			"Telefrag",DMG_THRUSTLESS|DMG_NO_PAIN
		);

		if(health>0){
			if(countinv("VileCount"))return super.damagemobj(
				inflictor,source,health+1,
				"NeedMoreVile",DMG_THRUSTLESS|DMG_NO_PAIN
			);

			//shields
			[shields,damage]=hdf.gothroughshields(shields,damage,inflictor,mod,flags);
			if(damage<1)return 0;
		}
		return super.damagemobj(
			inflictor,source,damage,mod,flags,angle
		);

	}
	override void postbeginplay(){
		super.postbeginplay();
		hdmobai.resize(self,0.95,1.05);
		shields=BE_SHIELDMAX;
	}
	states{
	spawn:
		BOSS AA 8 A_Look();
		BOSS A 1 A_SetTics(random(1,16));
		BOSS BB 8 A_Look();
		BOSS B 1 A_SetTics(random(1,16));
		BOSS CC 8 A_Look();
		BOSS C 1 A_SetTics(random(1,16));
		BOSS DD 8 A_Look();
		BOSS D 1 A_SetTics(random(1,16));
		TNT1 A 0 A_Jump(216,"spawn");
		TNT1 A 0 A_PlaySound("baron/active");
		loop;
	see:
		TNT1 A 0 A_JumpIfInventory("VileCount",1,"NotVileEnough");
		TNT1 A 0 A_AlertMonsters();
		BOSS ABCD 6{
			hdmobai.chase(self);
			if(health>0&&shields<BE_SHIELDMAX)shields+=3;
		}
		TNT1 A 0 A_JumpIfTargetInLOS("see");
		goto roam;
	roam:
		TNT1 A 0 A_AlertMonsters();
		TNT1 A 0 A_JumpIfTargetInLOS("missile");
		BOSS ABCD 8{
			hdmobai.wander(self,true);
			if(health>0&&shields<BE_SHIELDMAX)shields+=2;
			HealThing(random(1,2));
		}
		loop;
	missile:
		BOSS ABCD 3{
			A_FaceTarget(30);
			if(A_JumpIfTargetInLOS("shoot",10))setstatelabel("shoot");
		}
		BOSS E 0 A_JumpIfTargetInLOS("missile");
		goto see;
	shoot:
		BOSS E 0 A_AlertMonsters(0,AMF_TARGETNONPLAYER);
		BOSS E 0 A_Jump(64,2);
		BOSS E 0 A_JumpIfCloser(420,"MissileSweep");
		BOSS E 0 A_JumpIfHealthLower(BE_OKAY,1);
		goto MissileFuckYou;
		BOSS E 0 A_JumpIfHealthLower(BE_BAD,"MissileFuckYou");
		BOSS E 0 A_Jump(16,"MissileFuckYou");
		BOSS E 0 A_Jump(256,"MissileSkull","MissileMissile");
	MissileSkull:
		BOSS H 12 A_FaceTarget(0,0);
		BOSS H 12 bright{
			if(!random(0,2)){A_SpawnProjectile("BelphBall",28,0,0,2,pitch);}
			else A_BaronSoul();
		}
		BOSS H 18;
		goto MissileSweep;
	MissileMissile:
		BOSS H 16 A_FaceTarget(20,20);
		BOSS H 0 bright A_SpawnProjectile("BaleBall",38,0,2,0,0);
		BOSS H 6 bright A_SpawnProjectile("BaleBall",38,0,-2,0,0);
		BOSS H 0 bright A_SpawnProjectile("MiniBBall",46,0,9,2,0);
		BOSS H 6 bright A_SpawnProjectile("MiniBBall",46,0,-9,2,0);
		BOSS H 0 bright A_SpawnProjectile("MiniBBall",56,0,17,2,4);
		BOSS H 6 bright A_SpawnProjectile("MiniBBall",56,0,-17,2,4);
		BOSS H 0 bright A_SpawnProjectile("MiniBBall",66,0,24,2,7);
		BOSS H 6 bright A_SpawnProjectile("MiniBBall",66,0,-24,2,7);
		BOSS H 12;
		goto see;
	MissileFuckYou:
		BOSS H 18 A_FaceTarget(20,20);
		BOSS H 0 bright A_SpawnProjectile("BaleBall",38,0,2,0,0);
		BOSS H 0 bright A_SpawnProjectile("BaleBall",38,0,-2,0,0);
		BOSS H 6 bright{
			if(random(0,3))A_SpawnProjectile("BelphBall",28,0,0,2,pitch);
			else A_BaronSoul();
		}
		BOSS H 0 bright A_SpawnProjectile("MiniBBall",46,0,5,2,0);
		BOSS H 6 bright A_SpawnProjectile("MiniBBall",46,0,-5,2,0);
		BOSS H 0 bright A_SpawnProjectile("MiniBBall",56,0,7,2,4);
		BOSS H 6 bright A_SpawnProjectile("MiniBBall",56,0,-7,2,4);
		BOSS H 0 bright A_SpawnProjectile("MiniBBall",66,0,12,2,7);
		BOSS H 6 bright A_SpawnProjectile("MiniBBall",66,0,-12,2,7);
		BOSS H 12;
		goto see;
	pain:
		BOSS H 6 A_Pain();
		BOSS H 3 A_Jump(116,"see","MissileSkull");
	MissileSweep:
		BOSS F 4 A_FaceTarget(20,20);
		BOSS E 6;
		BOSS E 2 A_SpawnProjectile("MiniBBall",56,6,-6,CMF_AIMDIRECTION,pitch);
		BOSS F 2 A_SpawnProjectile("MiniBBall",46,4,-3,CMF_AIMDIRECTION,pitch);
		BOSS F 2 A_SpawnProjectile("MiniBBall",38,0,-1,CMF_AIMDIRECTION,pitch);
		BOSS G 2 A_SpawnProjectile("MiniBBall",32,0,1,CMF_AIMDIRECTION,pitch);
		BOSS G 2 A_SpawnProjectile("MiniBBall",32,0,3,CMF_AIMDIRECTION,pitch);
		BOSS G 2 A_SpawnProjectile("MiniBBall",32,0,6,CMF_AIMDIRECTION,pitch);
		BOSS G 6;
		BOSS E 2 A_Jump(194,"see");
		loop;
	melee:
		BOSS E 6 A_FaceTarget();
		BOSS F 2;
		BOSS G 6 A_CustomMeleeAttack(random(40,120),"baron/melee","","claws",true);
		BOSS F 5 A_JumpIf(getdistance(true,AAPTR_TARGET)>84,"missilesweep");
		goto see;
	death.telefrag:
		TNT1 A 0 spawn("Telefog",pos);
		TNT1 A 0 A_NoBlocking();
		TNT1 AAAAA 0 A_SpawnItemEx("BFGVileShard",
			frandom(-4,4),frandom(-4,4),frandom(6,24),
			frandom(1,6),0,frandom(1,3),
			frandom(0,360),SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS|SXF_SETMASTER
		);
		stop;
	death:
		---- A 0 A_JumpIfInventory("VileCount",1,"Death.NeedMoreVile");
		---- A 0 {
			hdmobai.corpseflags(self,false);
			A_Quake(2,64,0,600);
		}
		BOSS I 0 A_SpawnItemEx("tempshieldgreen",flags:SXF_SETMASTER);
		BOSS I 2 A_SpawnItemEx("BFGVileShard",0,0,20,10,0,8,45,SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS);
		BOSS I 2 A_SpawnItemEx("BFGVileShard",0,0,35,10,0,8,135,SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS);
		BOSS I 2 A_SpawnItemEx("BFGVileShard",0,0,50,10,0,8,225,SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS);
		BOSS I 2 A_SpawnItemEx("BFGVileShard",0,0,65,10,0,8,315,SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS);
		BOSS J 8 A_Scream();
		BOSS K 8 A_SpawnItemEx("tempshield2green",flags:SXF_SETMASTER);
		BOSS L 8 A_NoBlocking();
		BOSS M 8;
		BOSS N 8;
		BOSS O 30;
		BOSS O -1 A_BossDeath();
		stop;
	raise:
		---- A 0{
			hdmobai.corpseflags(self,false,true);
			A_GiveInventory("VileCount",1);
		}
		BOSS ONMLKJI 5;
		TNT1 A 0 A_JumpIfInventory("VileCount",5,"ReallyRaise");
		BOSS H 8;
		BOSS H 0 A_PlaySound("baron/sight");
		BOSS AAABB 3 A_Chase("NotVileEnough","NotVileEnough");
	NotVileEnough:
		BOSS H 0 A_StopSound(CHAN_BODY);
		BOSS H 0 A_Die("NeedMoreVile");
	Death.NeedMoreVile:
		BOSS J 5{
			hdmobai.corpseflags(self,false);
			A_PlaySound("misc/gibbed",CHAN_BODY);
			A_SpawnItemEx("tempshieldgreen",flags:SXF_SETMASTER);
		}
		BOSS K 5 A_SpawnItemEx("tempshield2green",flags:SXF_SETMASTER);
		BOSS L 5 A_NoBlocking();
		BOSS MN 5;
		BOSS O -1;
	ReallyRaise:
		BOSS H 5 A_TakeInventory("VileCount");
		BOSS H 0 A_PlaySound("baron/sight");
		goto see;
	}
}


class BelphBall:FastProjectile{
	default{
		+forcexybillboard +seekermissile +hittracer
		damagetype "thermal";
		decal "bigscorch";
		renderstyle "soultrans";
		alpha 0.05;
		scale 0.6;
		radius 12;
		height 13;
		speed 2;
		damage 8;
		seesound "skull/melee";
		deathsound "bakaslave/death";
	}
	states{
	spawn:
		SKUL JIHGF 3 bright A_FadeIn(0.2);
		SKUL F 0 bright A_ScaleVelocity(32);
	see:
		SKUL C 2 bright;
		SKUL C 1 bright A_SpawnItemEx("ImpBallTail",-6,0,12,vel.x*0.8,vel.y*0.8,vel.z*0.8,0,SXF_NOCHECKPOSITION|SXF_ABSOLUTEMOMENTUM);
		SKUL D 2 bright;
		SKUL D 1 bright A_SpawnItemEx("ImpBallTail",-6,0,12,vel.x*0.8,vel.y*0.8,vel.z*0.8,0,SXF_NOCHECKPOSITION|SXF_ABSOLUTEMOMENTUM);
		loop;
	death:
		SKUL FFFFFF 0 A_SpawnItemEx("HDSmoke",0,0,random(-2,4),frandom(-2,2),frandom(-2,2),random(3,5),0,SXF_NOCHECKPOSITION);
		SKUL F 1 bright A_Quake(3,28,0,128);
		SKUL G 1 bright A_Explode(56,96,1);
		SKUL GGGGGGGGGGG 0 A_SpawnItemEx("BigWallChunk",0,0,random(-4,4),random(-10,10),random(-10,10),random(-2,10),random(0,360),SXF_NOCHECKPOSITION);
		SKUL HIJ 1 bright A_FadeOut(0.2);
		TNT1 AAAAA random(2,3) A_SpawnItemEx("HDSmoke",0,0,random(-2,4),frandom(-2,2),frandom(-2,2),random(3,5),0,SXF_NOCHECKPOSITION);
		stop;
	}
}

class MiniBBallTail:HDActor{
	default{
		+nointeraction
		+forcexybillboard
		renderstyle "add";
		alpha 0.6;
		scale 0.7;
	}
	states{
	spawn:
		BAL7 E 2 bright A_FadeOut(0.2);
		TNT1 A 0 A_PlaySound("weapons/plasidle",0,0.4,0,6);
		loop;
	}
}

class MiniBBall:HDActor{
	default{
		+forcexybillboard
		projectile;
		+seekermissile
		damagetype "balefire";
		renderstyle "add";
		decal "gooscorch";
		alpha 0.8;
		scale 0.6;
		radius 4;
		height 6;
		speed 16;
		damage 6;
		seesound "prince/attack";
		deathsound "prince/shotx";
	}
	int user_counter;
	states{
	spawn:
		BAL7 EDC 1 bright;
		BAL7 ABABA 2 bright;
		BAL7 BAB 3 bright;
	spawn2:
		BAL7 A 2 bright A_SeekerMissile(5,10);
		BAL7 B 2 bright A_SpawnItemEx("MiniBBallTail",-3,0,3,3,0,random(1,2),0,161,0);
		BAL7 A 2 bright A_SeekerMissile(5,9);
		BAL7 B 2 bright A_SpawnItemEx("MiniBBallTail",-3,0,3,3,0,random(1,2),0,161,0);
		BAL7 A 2 bright A_SeekerMissile(4,8);
		BAL7 B 2 bright A_SpawnItemEx("MiniBBallTail",-3,0,3,3,0,random(1,2),0,161,0);
		BAL7 A 2 bright A_SeekerMissile(3,6);
		BAL7 B 2 bright A_SpawnItemEx("MiniBBallTail",-3,0,3,3,0,random(1,2),0,161,0);
	spawn3:
		TNT1 A 0 A_JumpIf(user_counter>4,"spawn4");
		TNT1 A 0 {user_counter++;}
		BAL7 A 3 bright A_SeekerMissile(1,1);
		BAL7 B 3 bright A_SpawnItemEx("MiniBBallTail",-3,0,3,3,0,random(1,2),0,161,0);
		loop;
	spawn4:
		BAL7 A 3 bright A_SpawnItemEx("MiniBBallTail",-3,0,3,3,0,random(1,2),0,161,0);
		TNT1 A 0 A_JumpIf(pos.z-floorz<10,2);
		BAL7 B 3 bright A_ChangeVelocity(frandom(-0.2,1),frandom(-1,1),frandom(-1,0.9),CVF_RELATIVE);
		loop;
		BAL7 B 3 bright A_ChangeVelocity(frandom(-0.2,1),frandom(-1,1),frandom(-0.6,1.9),CVF_RELATIVE);
		loop;
	death:
		BAL7 CDE 4 bright A_FadeOut(0.2);
		stop;
	}
}
