// ------------------------------------------------------------
// 7.76mm Ammo
// ------------------------------------------------------------
class LiberatorMag:Ammo{
	default{
		+inventory.ignoreskill
		ammo.backpackmaxamount 12;ammo.backpackamount 0;
		inventory.maxamount 5;
		scale 0.7;
		inventory.pickupmessage "Picked up a 7.76mm magazine.";
	}states{
	spawn:
		TNT1 A 0 nodelay{
			HDF.SplitAmmoSpawn(self);
			A_SpawnItemEx("LiberatorMagP",0,0,0,
				vel.x,vel.y,vel.z,
				0,SXF_NOCHECKPOSITION|SXF_ABSOLUTEMOMENTUM
			);
		}stop;
	}
}
class SevenMilAmmo:Ammo{
	default{
		+forcexybillboard +cannotpush
		+inventory.ignoreskill
		ammo.backpackmaxamount 80;ammo.backpackamount 0;
		height 16; radius 8;
		inventory.maxamount 40;
		scale 0.7;
		inventory.pickupmessage "Picked up a 7.76mm round.";
	}
	states{
	spawn:
		RBRS A -1 nodelay{HDF.SplitAmmoSpawn(self);}
	}
}
class HDSpent7mm:HDDebris{
	override void postbeginplay(){
		super.postbeginplay();
		A_ChangeVelocity(random(-3,3),frandom(-0.4,0.4),0,CVF_RELATIVE);
	}
	default{
		bouncesound "misc/casing";scale 0.7;
	}
	states{
	spawn:
		RBRS A 2{angle+=45;}
		loop;
	death:
		RBRS A -1{
			actor p=spawn("SevenMilBrass",self.pos,ALLOW_REPLACE);
			p.angle=self.angle;p.vel=self.vel;
			destroy();
		}stop;
	}
}
class SevenMilBrass:Ammo{
	default{
		+inventory.ignoreskill +forcexybillboard +cannotpush
		height 16;radius 8;ammo.backpackamount 0;
		ammo.backpackmaxamount 120;
		inventory.maxamount 60;
		scale 0.7;
		inventory.pickupmessage "Picked up some brass.";
	}
	states{
	spawn:
		RBRS A -1 nodelay{
			HDF.SplitAmmoSpawn(self);
			A_ChangeVelocity(0,frandom(-1,1),0,CVF_RELATIVE);
		}stop;
	}
}
class HDLoose7mm:HDSpent7mm{
	override void postbeginplay(){
		HDDebris.postbeginplay();
	}
	default{
		bouncefactor 0.6;
	}
	states{
	death:
		---- A 1{
			actor a=spawn("SevenMilAmmo",pos,ALLOW_REPLACE);
			a.angle=self.angle;a.vel=self.vel;
			destroy();
		}stop;
	}
}


class LiberatorEmptyMag:HD4mmMagEmpty{
	default{scale 0.7;}
	states{
	spawn:
		RMAG B -1;
		stop;
	death:
		---- A -1{bmissile=true;}
		stop;
	}
}
class LiberatorMagP:HDUPK replaces LiberatorMag{
	default{
		stamina 30;
		scale 0.7;
	}
	override void postbeginplay(){
		super.postbeginplay();
		if(stamina<1){
			actor emg=spawn("LiberatorEmptyMag",pos);
			if(emg)emg.vel=vel;
			destroy();return;
		}
	}
	states{
	spawn:
		RMAG A -1 A_Stop();
	give:
		---- A 0{
			if(stamina>=30){    
				if(A_JumpIfInTargetInventory("LiberatorMag",0,"null")){
					setstatelabel("spawn");return;
				}else{
					target.A_GiveInventory("LiberatorMag",1);
					destroy();return;
				}
			}else{
				int al=target.countinv("SevenMilAmmo");
				target.A_GiveInventory("SevenMilAmmo",stamina);
				stamina-=target.countinv("SevenMilAmmo")-al;
				if(!stamina){destroy();return;}
				else setstatelabel("spawn");
			}
		}stop;
	}
}


