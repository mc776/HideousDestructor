// ------------------------------------------------------------
// Shotgun Shells
// ------------------------------------------------------------
class HDShellAmmo:Ammo{
	default{
		+inventory.ignoreskill
		inventory.maxamount 24;
		ammo.backpackmaxamount 150;
		ammo.backpackamount 0;
		inventory.pickupmessage "Picked up a shotgun shell.";
		scale 0.3;
	}
	states{
	spawn:
		SHL1 A -1;
	}
}
class HDSpentShell:Actor{
	default{
		+missile +doombounce +bounceonactors +cannotpush -noteleport +forcexybillboard
		seesound "misc/casing2";scale 0.3;height 2;radius 2;
	}
	states{
	spawn:
		ESHL ABCDEFGH 2;
		loop;
	death:
		ESHL A -1{
			bmissile=false;
			frame=randompick(0,0,0,0,4,4,4,4,2,2,5);
		}stop;
	}
}
class HDUnSpentShell:HDSpentShell{
	states{
	spawn:
		ESHL ABCDE 2;
		TNT1 A 0{
			if(A_JumpIfInTargetInventory("HDShellAmmo",0,"null"))
			A_SpawnItemEx("HDFumblingShell",
				0,0,0,vel.x+frandom(-1,1),vel.y+frandom(-1,1),vel.z,
				0,SXF_NOCHECKPOSITION|SXF_ABSOLUTEMOMENTUM
			);else A_GiveToTarget("HDShellAmmo",1);
		}
		stop;
	}
}
class HDFumblingShell:HDSpentShell{
	states{
	spawn:
		ESHL ABCDEFGH 2;
		loop;
	death:
		TNT1 A 0 A_SpawnItemEx("HDFumbledShell",0,0,0,
			vel.x,vel.y,vel.z,
			0,SXF_ABSOLUTEMOMENTUM|SXF_NOCHECKPOSITION
		);stop;
	}
}
class HDFumbledShell:HDShellAmmo{
	default{
		+cannotpush
		scale 0.3;
		inventory.pickupmessage "Picked up a shotgun shell.";
	}
	states{
	spawn:
		ESHL A -1 nodelay{
			bmissile=false;
			frame=randompick(0,0,0,0,4,4,4,4,2,2,5);
		}stop;
	}
}


class ShellBoxPickup:HDUPK{
	default{
		scale 0.4;
		stamina 20;
		activesound "weapons/pocket";
		hdupk.pickupmessage "Picked up some shotgun shells.";
		hdupk.pickuptype "HDShellAmmo";
		translation "160:167=80:95";
	}
	states{
	spawn:
		SBOX A -1 A_Stop();
	}
}
class ShellPickup:ShellBoxPickup{
	default{
		scale 0.3;
		stamina 4;
	}
	states{
	spawn:
		SHEL A 0 A_Stop();
		SHEL A -1{
			if(stamina<4){
				while(stamina>0){
					stamina--;
					A_SpawnItemEx("HDShellAmmo",0,random(-1,1),0,
						random(3,3),0,0,random(-3,3),SXF_NOCHECKPOSITION
					);
				}
				destroy();
			}
		}
	}
}
