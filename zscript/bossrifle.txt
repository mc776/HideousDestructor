// ------------------------------------------------------------
// Bolt-Action Rifle
// ------------------------------------------------------------
class BossRifleSpawner:IdleDummy{
	states{
	spawn:
		TNT1 A 0 nodelay{
			A_SpawnItemEx("HD7mClip",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION);
			A_SpawnItemEx("HD7mClip",3,0,0,0,0,0,0,SXF_NOCHECKPOSITION);
			A_SpawnItemEx("HD7mClip",1,0,0,0,0,0,0,SXF_NOCHECKPOSITION);
			A_SpawnItemEx("BossRifle",-3,0,0,0,0,0,0,SXF_NOCHECKPOSITION);
		}stop;
	}
}
class BossRifle:HDWeapon{
	default{
		weapon.slotnumber 8;
		weapon.kickback 15;
		weapon.selectionorder 80;
		inventory.pickupSound "misc/w_pkup";
		inventory.pickupMessage "You got the bolt-action rifle!";
		weapon.bobrangex 0.28;
		weapon.bobrangey 1.1;
		scale 0.75;
		Obituary "%o sure showed %k who was the boss!";
		hdweapon.barrelsize 40,1,2;
		hdweapon.refid HDLD_BOSS;
		hdweapon.nicename "Boss Rifle";
		inventory.maxamount 3; //1 use user setting; 2 custom chamber; 3 regular
	}
	action void A_ChamberGrit(int amt,bool onlywhileempty=false){
		int ibg=invoker.weaponstatus[BOSSS_GRIME];
		bool customchamber=(invoker.weaponstatus[0]&BOSSF_CUSTOMCHAMBER);
		if(amt>0&&customchamber)amt>>=1;
		if(!onlywhileempty||invoker.weaponstatus[BOSSS_CHAMBER]<1)ibg+=amt;
		else if(!random(0,4))ibg++;
		invoker.weaponstatus[BOSSS_GRIME]=clamp(ibg,0,100);
		//if(hd_debug)A_Log(string.format("Boss grit level: %i",invoker.weaponstatus[BOSSS_GRIME]));
	}
	int pickuprounds;
	override void tick(){
		super.tick();
		drainheat(BOSSS_HEAT,4);
	}
	override void postbeginplay(){
		super.postbeginplay();

		switch(amount){
		case 3:
			weaponstatus[0]&=~BOSSF_CUSTOMCHAMBER;
			break;
		case 2:
			weaponstatus[0]|=BOSSF_CUSTOMCHAMBER;
			break;
		default:
			if(
				!random(0,31)
				||(
					owner&&owner.player
					&&cvar.getcvar("hd_bosscustomchamber",owner.player).getbool()
				)
			){
				weaponstatus[0]|=BOSSF_CUSTOMCHAMBER;
			}else weaponstatus[0]&=~BOSSF_CUSTOMCHAMBER;
			break;
		}
		amount=1;maxamount=1;

		if(weaponstatus[0]&BOSSF_CUSTOMCHAMBER)weaponstatus[BOSSS_GRIME]=random(0,5);
		else weaponstatus[BOSSS_GRIME]=random(0,10);

		if(owner&&owner.player){
			if(cvar.getcvar("hd_bossfrontreticle",owner.player).getbool())
				weaponstatus[0]|=BOSSF_FRONTRETICLE;
				else weaponstatus[0]&=~BOSSF_FRONTRETICLE;
			if(weaponstatus[0]&BOSSF_FRONTRETICLE)weaponstatus[BOSSS_ZOOM]=
				clamp(cvar.getcvar("hd_libzoom",owner.player).getint(),12,40);
				else weaponstatus[BOSSS_ZOOM]=
				clamp(cvar.getcvar("hd_libzoom",owner.player).getint(),5,60);
			weaponstatus[BOSSS_DROPADJUST]=
				clamp(cvar.getcvar("hd_bossmoa",owner.player).getint(),0,600);
		}
	}
	override double gunmass(){
		return 12;
	}
	override double weaponbulk(){
		return 144+weaponstatus[BOSSS_MAG]*ENC_776_LOADED;
	}
	override void GunBounce(){
		super.GunBounce();
		weaponstatus[BOSSS_GRIME]+=random(-7,3);
		if(weaponstatus[BOSSS_CHAMBER]>2&&!random(0,7))weaponstatus[BOSSS_CHAMBER]-=2;
	}
	override void DrawHUDStuff(HDStatusBar sb,HDWeapon hdw,HDPlayerPawn hpl){
		if(sb.gimmehud){
			int nextmagloaded=sb.GetNextLoadMag(hdmagammo(hpl.findinventory("HD7mClip")));
			if(nextmagloaded<1){
				sb.drawimage("RCLPF0",(-58,-3),sb.DI_SCREEN_CENTER_BOTTOM,alpha:nextmagloaded?0.6:1.,scale:(1.6,1.6));
			}else if(nextmagloaded<3){
				sb.drawimage("RCLPE0",(-58,-3),sb.DI_SCREEN_CENTER_BOTTOM,scale:(1.6,1.6));
			}else if(nextmagloaded<5){
				sb.drawimage("RCLPD0",(-58,-3),sb.DI_SCREEN_CENTER_BOTTOM,scale:(1.6,1.6));
			}else if(nextmagloaded<7){
				sb.drawimage("RCLPC0",(-58,-3),sb.DI_SCREEN_CENTER_BOTTOM,scale:(1.6,1.6));
			}else if(nextmagloaded<9){
				sb.drawimage("RCLPB0",(-58,-3),sb.DI_SCREEN_CENTER_BOTTOM,scale:(1.6,1.6));
			}else sb.drawimage("RCLPA0",(-58,-3),sb.DI_SCREEN_CENTER_BOTTOM,scale:(1.6,1.6));
			sb.drawnum(hpl.countinv("HD7mClip"),-45,-8,sb.DI_SCREEN_CENTER_BOTTOM,font.CR_BLACK);
		}
		sb.drawwepnum(hdw.weaponstatus[BOSSS_MAG],10);
		sb.drawwepcounter(hdw.weaponstatus[BOSSS_CHAMBER],
			-16,-10,"blank","RBRSA1A5","RBRSA3A7","RBRSA4A6"
		);
		sb.drawnum(hdw.weaponstatus[BOSSS_ZOOM],
			-30,-18,sb.DI_SCREEN_CENTER_BOTTOM|sb.DI_TEXT_ALIGN_RIGHT,Font.CR_DARKGRAY
		);
		sb.drawnum(hdw.weaponstatus[BOSSS_DROPADJUST],
			-16,-18,sb.DI_SCREEN_CENTER_BOTTOM|sb.DI_TEXT_ALIGN_RIGHT,Font.CR_WHITE
		);
	}

	override void DrawSightPicture(
		HDStatusBar sb,HDWeapon hdw,HDPlayerPawn hpl,
		bool sightbob,vector2 bob,double fov,bool scopeview,actor hpc,string whichdot
	){
		int cx,cy,cw,ch;
		[cx,cy,cw,ch]=Screen.GetClipRect();
		sb.SetClipRect(
			-16+bob.x,-4+bob.y,32,16,
			sb.DI_SCREEN_CENTER
		);
		vector2 bobb=bob*2;
		bobb.y=clamp(bobb.y,-8,8);
		sb.drawimage(
			"frntsite",(0,0)+bobb,sb.DI_SCREEN_CENTER|sb.DI_ITEM_TOP,
			alpha:0.9,scale:(1.6,2)
		);
		sb.SetClipRect(cx,cy,cw,ch);
		sb.drawimage(
			"backsite",(0,0)+bob,sb.DI_SCREEN_CENTER|sb.DI_ITEM_TOP,
			scale:(2,1)
		);

		if(scopeview){
			int scaledyoffset=60;
			int scaledwidth=89;
			vector2 sclhalf=(0.5,0.5);
			double degree=0.1*hdw.weaponstatus[BOSSS_ZOOM];
			double deg=1/degree;
			int cx,cy,cw,ch;
			[cx,cy,cw,ch]=screen.GetClipRect();
			sb.SetClipRect(-44+bob.x,16+bob.y,scaledwidth,scaledwidth,
				sb.DI_SCREEN_CENTER);
			texman.setcameratotexture(hpc,"HDXHCAM3",degree);
			sb.drawimage(
				"HDXHCAM3",(0,scaledyoffset)+bob,
				sb.DI_SCREEN_CENTER|sb.DI_ITEM_CENTER,
				scale:sclhalf
			);
			if(hdw.weaponstatus[0]&BOSSF_FRONTRETICLE){
				sb.drawimage(
					"reticle1",(0,scaledyoffset)+bob*deg*5,
					sb.DI_SCREEN_CENTER|sb.DI_ITEM_CENTER,
					scale:(1.6,1.6)*deg
				);
			}else{
				sb.drawimage(
					"reticle1",(0,scaledyoffset)+bob,
					sb.DI_SCREEN_CENTER|sb.DI_ITEM_CENTER,
					scale:sclhalf
				);
			}
			sb.drawimage(
				"scophole",(0,scaledyoffset)+bob*5,sb.DI_SCREEN_CENTER|sb.DI_ITEM_CENTER,
				scale:(1.5,1.5)
			);
			screen.SetClipRect(cx,cy,cw,ch);
			sb.drawimage(
				"libscope",(0,scaledyoffset)+bob,sb.DI_SCREEN_CENTER|sb.DI_ITEM_CENTER,
				scale:(1.24,1.24)
			);
			sb.drawnum(degree*10,
				4+bob.x,105+bob.y,sb.DI_SCREEN_CENTER,Font.CR_BLACK
			);
			sb.drawnum(hdw.weaponstatus[BOSSS_DROPADJUST],
				6+bob.x,9+bob.y,sb.DI_SCREEN_CENTER,Font.CR_BLACK
			);
			sb.drawimage(
				"BLETA0",(0.8,109)+bob,sb.DI_SCREEN_CENTER|sb.DI_ITEM_CENTER,
				alpha:0.6,scale:(1.3,1.3)
			);
		}
		// the scope display is in 10ths of an arcminute.
		// one dot = 6 arcminutes.
	}
	override void consolidate(){
		weaponstatus[BOSSS_GRIME]=random(20,40);
	}
	override void DropOneAmmo(int amt){
		if(owner){
			amt=clamp(amt,1,10);
			if(owner.countinv("SevenMilAmmo"))owner.A_DropInventory("SevenMilAmmo",10);
			else owner.A_DropInventory("HD7mClip",1);
		}
	}
	states{
	select0:
		BARG A 0;
		goto select0bfg;
	deselect0:
		BARG A 0;
		goto deselect0big;

	ready:
		BARG A 1{
			if(pressingzoom()){
				if(player.cmd.buttons&BT_USE){
					A_ZoomAdjust(BOSSS_DROPADJUST,0,600,BT_USE);
				}else if(invoker.weaponstatus[0]&BOSSF_FRONTRETICLE)A_ZoomAdjust(BOSSS_ZOOM,12,40);
				else A_ZoomAdjust(BOSSS_ZOOM,5,60);
				A_WeaponReady(WRF_NONE);
			}else A_WeaponReady(WRF_ALL);
		}goto readyend;
	user3:
		---- A 0 A_MagManager("HD7mClip");
		goto ready;
	fire:
		BARG A 1 A_JumpIf(invoker.weaponstatus[BOSSS_CHAMBER]==2,"shoot");
		goto ready;
	shoot:
		BARG A 1{
			A_Gunflash();
			invoker.weaponstatus[BOSSS_CHAMBER]=1;
			A_PlaySound("weapons/bigrifle2",6);
			A_AlertMonsters();

			actor p=spawn("HDBullet776",pos+(0,0,height-6),ALLOW_REPLACE);
			p.target=self;p.angle=angle;p.pitch=pitch;
			p.speed*=(invoker.weaponstatus[0]&BOSSF_CUSTOMCHAMBER)?1.06:1.27;
			p.vel+=self.vel;
			p.pitch-=(1./600.)*invoker.weaponstatus[BOSSS_DROPADJUST];

			A_MuzzleClimb(
				0,0,
				-frandom(0.2,0.4),-frandom(0.6,1.),
				-frandom(0.4,0.7),-frandom(1.2,2.1),
				-frandom(0.4,0.7),-frandom(1.2,2.1)
			);
		}
		BARG F 1;
		BARG F 1 A_JumpIf(gunbraced(),"ready");
		goto ready;
	flash:
		BARF A 1 bright{
			A_Light1();
			HDFlashAlpha(-96);
			A_ZoomFactor(0.95,ZOOM_INSTANT|ZOOM_NOSCALETURNING);
			A_ChamberGrit(randompick(0,0,0,0,0,1,1,1,1,-1));
		}
		TNT1 A 1{
			A_Light0();
			A_ZoomFactor(1.0,ZOOM_INSTANT|ZOOM_NOSCALETURNING);
		}stop;
	altfire:
		BARG A 2 offset(0,34);
		BARG B 1 offset(2,36) A_WeaponBusy();
		BARG B 1 offset(4,38){
			if(invoker.weaponstatus[BOSSS_CHAMBER]>2)setweaponstate("jamderp");
		}
		BARG B 1 offset(0,34);
		BARG B 0 A_Refire("chamber");
		goto ready;
	althold:
		BARG E 1 A_WeaponReady(WRF_NOFIRE);
		BARG E 1{
			A_ClearRefire();
			bool eww=invoker.weaponstatus[BOSSS_GRIME]>30;
			bool chempty=!invoker.weaponstatus[BOSSS_CHAMBER];
			if(pressingunload()){
				if(chempty||eww){
					return resolvestate("altholdclean");
				}else{
					invoker.weaponstatus[0]|=BOSSF_UNLOADONLY;
					return resolvestate("loadchamber");
				}
			}else if(pressingreload()){
				if(chempty&&!eww&&countinv("SevenMilAmmo")){
					invoker.weaponstatus[0]&=~BOSSF_UNLOADONLY;
					return resolvestate("loadchamber");
				}else if(eww){
					return resolvestate("altholdclean");
				}
			}
			if(pressingaltfire())return resolvestate("althold");
			return resolvestate("altholdend");
		}
	altholdend:
		BARG E 0 A_ChamberGrit(randompick(0,0,0,0,-1,1,2),true);
		BARG E 0 A_PlaySound("weapons/boltfwd",CHAN_WEAPON);
		BARG DCB 3 A_WeaponReady(WRF_NOFIRE);
		goto ready;
	loadchamber:
		BARG E 0 A_JumpIf(invoker.weaponstatus[BOSSS_GRIME]>10,"altholdclean");
		BARG E 1 offset(2,36) A_ClearRefire();
		BARG E 1 offset(3,38);
		BARG E 1 offset(5,42);
		BARG E 1 offset(8,48) A_PlaySound("weapons/pocket",CHAN_WEAPON);
		BARG E 1 offset(9,52) A_MuzzleClimb(frandom(-0.2,0.2),0.2,frandom(-0.2,0.2),0.2,frandom(-0.2,0.2),0.2);
		BARG E 2 offset(8,60);
		BARG E 2 offset(7,72);
		TNT1 A 18 A_PlaySound("weapons/pocket",CHAN_WEAPON);
		TNT1 A 4{
			A_PlaySound("weapons/bossload",CHAN_WEAPON,0.7);
			if(invoker.weaponstatus[0]&BOSSF_UNLOADONLY){
				int chm=invoker.weaponstatus[BOSSS_CHAMBER];
				invoker.weaponstatus[BOSSS_CHAMBER]=0;
				if(chm<2||A_JumpIfInventory("SevenMilAmmo",0,"null")){
					class<actor> whatkind=chm==2?"HDLoose7mm":"HDSpent7mm";
					actor rrr=spawn(whatkind,pos+(cos(angle)*10,sin(angle)*10,height-12),ALLOW_REPLACE);
					rrr.angle=angle;rrr.A_ChangeVelocity(1,2,1,CVF_RELATIVE);
				}else HDF.Give(self,"SevenMilAmmo",1);
				A_ChamberGrit(randompick(0,0,0,0,-1,1),true);
			}else{
				A_TakeInventory("SevenMilAmmo",1,TIF_NOTAKEINFINITE);
				invoker.weaponstatus[BOSSS_CHAMBER]=2;
			}
		} 
		BARG E 2 offset(7,72);
		BARG E 2 offset(8,60);
		BARG E 1 offset(7,52);
		BARG E 1 offset(5,42);
		BARG E 1 offset(3,38);
		BARG E 1 offset(3,35);
		goto althold;
	altholdclean:
		BARG E 1 offset(2,36) A_ClearRefire();
		BARG E 1 offset(3,38);
		BARG E 1 offset(5,42) A_Log("Looking inside that chamber...",true);
		BARG E 1 offset(8,48) A_PlaySound("weapons/pocket",CHAN_WEAPON);
		BARG E 1 offset(7,52) A_MuzzleClimb(frandom(-0.2,0.2),0.2,frandom(-0.2,0.2),0.2,frandom(-0.2,0.2),0.2);
		TNT1 A 3 A_PlaySound("weapons/pocket",CHAN_WEAPON);
		TNT1 AAAA 4 A_MuzzleClimb(frandom(-0.2,0.2),frandom(-0.2,0.2),frandom(-0.2,0.2),frandom(-0.2,0.2),frandom(-0.2,0.2),frandom(-0.2,0.2),frandom(-0.2,0.2),frandom(-0.2,0.2));
		TNT1 A 3 A_PlaySound("weapons/pocket",CHAN_WEAPON);
		TNT1 AAAA 4 A_MuzzleClimb(frandom(-0.2,0.2),frandom(-0.2,0.2),frandom(-0.2,0.2),frandom(-0.2,0.2),frandom(-0.2,0.2),frandom(-0.2,0.2),frandom(-0.2,0.2),frandom(-0.2,0.2));
		TNT1 A 40{
			A_PlaySound("weapons/pocket",CHAN_WEAPON);
			int amt=invoker.weaponstatus[BOSSS_GRIME];
			string amts="There doesn't seem to be much. ";
			if(amt>40)amts="What the FUCK. ";
			else if(amt>30)amts="About time - this gun is barely functional. ";
			else if(amt>20)amts="This is starting to gum up badly. ";
			else if(amt>10)amts="It can use some cleaning. ";

			static const string cleanverbs[]={"extract","scrape off","wipe away","carefully remove","dump out","pick out","blow off","shake out","scrub off","fish out"};
			static const string contaminants[]={"some dust","a lot of dust","a bit of powder residue","a disturbing amount of powder residue","some excess grease","a layer of soot","some iron filings","a bit of hair","an eyelash","a patch of dried blood","a bit of rust","a crumb","a dead insect","ashes","some loose bits of skin","a sticky fluid of some sort","wow some fucking *gunk*","a booger","trace fecal matter","yet even more of that anonymous grey debris that all those bullet impacts make","a dollop of strawberry jam","a tiny cancerous nodule of Second Flesh","some crystalline buildup of congealed Frag","a nesting queen space ant","a single modern-day transistor","a tiny Boss rifle (also jammed)","a colourless film of darkness made visible"};
			static const string actionparts[]={"bolt carrier","main extractor","auxiliary extractor","cam pin","bolt head","striker","firing pin spring","ejector slot","striker spring","ejector spring"};
			for(int i=amt;i>0;i-=random(8,16))amts.appendformat("You %s %s from the %s. ",
				cleanverbs[random(0,cleanverbs.size()-1)],
				contaminants[random(0,random(0,contaminants.size()-1))],
				actionparts[random(0,random((actionparts.size()>>1),actionparts.size()-1))]
			);
			amts.appendformat("\n");

			amt=randompick(-3,-5,-5,-random(8,16));

			A_ChamberGrit(amt,true);
			amt=invoker.weaponstatus[BOSSS_GRIME];
			if(amt>40)amts.appendformat("You barely scrape the surface of this all-encrusting abomination.");
			else if(amt>30)amts.appendformat("The gun will need a lot more work than this before it can be deployed again.");
			else if(amt>20)amts.appendformat("You might get a few shots out of it now.");
			else if(amt>10)amts.appendformat("It's better, but still not good.");
			else amts.appendformat("Good to go.");
			A_Log(amts,true);
		}
		BARG E 1 offset(7,52);
		BARG E 1 offset(8,48);
		BARG E 1 offset(5,42);
		BARG E 1 offset(3,38);
		BARG E 1 offset(2,36);
		goto althold;
	jam:
		BARG A 0{
			int chm=invoker.weaponstatus[BOSSS_CHAMBER];
			if(chm<1)setweaponstate("chamber");
			else if(chm<3)invoker.weaponstatus[BOSSS_CHAMBER]+=2;
		}
	jamderp:
		BARG A 0 A_PlaySound("weapons/rifleclick",CHAN_WEAPON);
		BARG D 1 offset(4,38);
		BARG D 2 offset(2,36);
		BARG D 2 offset(4,38)A_MuzzleClimb(frandom(-0.5,0.6),frandom(-0.3,0.6));
		BARG D 3 offset(2,36){
			A_MuzzleClimb(frandom(-0.5,0.6),frandom(-0.3,0.6));
			if(!random(0,3)){
				setweaponstate("chamber");
				if(invoker.weaponstatus[BOSSS_CHAMBER]>2)  
					invoker.weaponstatus[BOSSS_CHAMBER]-=2;
			}
		}
		BARG D 2 offset(4,38);
		BARG D 3 offset(2,36);
		BARG A 0 A_Refire("jamderp");
		goto ready;
	chamber:
		BARG C 2 offset(4,38){
			if(
				random(0,max(2,invoker.weaponstatus[BOSSS_GRIME]>>3))
				&&invoker.weaponstatus[BOSSS_CHAMBER]>2
			){
				invoker.weaponstatus[BOSSS_CHAMBER]+=2;
				A_MuzzleClimb(
					-frandom(0.6,2.3),-frandom(0.6,2.3),
					-frandom(0.6,1.3),-frandom(0.6,1.3),
					-frandom(0.6,1.3),-frandom(0.6,1.3)
				);
				setweaponstate("jamderp");
			}else A_PlaySound("weapons/boltback",CHAN_WEAPON);
		}
		BARG D 2 offset(6,42);
		BARG D 1 offset(6,42){
			if(gunbraced())A_MuzzleClimb(
				frandom(-0.1,0.3),frandom(-0.1,0.3)
			);else A_MuzzleClimb(
				frandom(-0.2,0.8),frandom(-0.4,0.8)
			);
			int jamchance=
				invoker.weaponstatus[BOSSS_GRIME]
				+(invoker.weaponstatus[BOSSS_HEAT]>>2)
				+invoker.weaponstatus[BOSSS_CHAMBER]
			;
			if(hd_debug)A_Log("jam chance: "..jamchance);
			if(random(0,100)<jamchance)setweaponstate("jam");
		}
		BARG D 2 offset(6,42){
			//eject
			int chm=invoker.weaponstatus[BOSSS_CHAMBER];
			if(chm>1){  
				A_SpawnItemEx(
					"HDLoose7mm",cos(pitch)*8,1,height-7-sin(pitch)*8,
					cos(pitch)*cos(angle-80)*4+vel.x,
					cos(pitch)*sin(angle-80)*4+vel.y,
					-sin(pitch)*4+vel.z,
					0,SXF_ABSOLUTEMOMENTUM|SXF_NOCHECKPOSITION|SXF_TRANSFERPITCH
				);
			}else if(chm==1){
				A_SpawnItemEx(
					"HDSpent7mm",cos(pitch)*8,1,height-7-sin(pitch)*8,
					cos(pitch)*cos(angle-80)*6+vel.x,
					cos(pitch)*sin(angle-80)*6+vel.y,
					-sin(pitch)*6+vel.z,
					0,SXF_ABSOLUTEMOMENTUM|SXF_NOCHECKPOSITION|SXF_TRANSFERPITCH
				);
			}
			//cycle new
			if(invoker.weaponstatus[BOSSS_MAG]>0){  
				invoker.weaponstatus[BOSSS_CHAMBER]=2;
				invoker.weaponstatus[BOSSS_MAG]--;
			}else invoker.weaponstatus[BOSSS_CHAMBER]=0;
		}
		BARG E 1 offset(6,42) A_WeaponReady(WRF_NOFIRE);
		BARG E 0 A_Refire("althold");
		BARG E 1 offset(3,34) A_WeaponReady(WRF_NOFIRE);
		BARG E 1 offset(3,34) A_PlaySound("weapons/boltfwd",CHAN_WEAPON);
		BARG E 3 offset(4,38);
		BARG D 2 offset(3,37){
			if(!gunbraced())
				A_MuzzleClimb(frandom(-0.4,0.8),frandom(-0.2,0.8));
		}
		BARG C 3 offset(2,36);
		BARG B 3 offset(0,34);
		goto ready;
	reload:
		BARG A 1 offset(0,34);
		BARG A 1 offset(2,36);
		BARG A 1 offset(4,40);
		BARG A 2 offset(8,42){
			A_PlaySound("weapons/rifleclick2",CHAN_WEAPON);
			A_MuzzleClimb(-frandom(0.4,0.8),frandom(0.4,1.4));
		}
		BARG A 4 offset(14,46){
			A_PlaySound("weapons/rifleload",CHAN_WEAPON);
			A_MuzzleClimb(-frandom(0.4,0.8),frandom(0.4,1.4));
		}
		BARG A 0{
			int mg=invoker.weaponstatus[BOSSS_MAG];
			if(mg==10)setweaponstate("reloaddone");
			else if(
				mg<1
				&&!HDMagAmmo.NothingLoaded(self,"HD7mClip")
			)setweaponstate("loadclip");
		}
	loadhand:
		BARG A 0 A_JumpIfInventory("SevenMilAmmo",1,"loadhandloop");
		goto reloaddone;
	loadhandloop:
		BARG A 4{
			int hnd=min(
				countinv("SevenMilAmmo"),3,
				10-invoker.weaponstatus[BOSSS_MAG]
			);
			if(hnd<1){
				setweaponstate("reloaddone");
				return;
			}else{
				A_TakeInventory("SevenMilAmmo",hnd,TIF_NOTAKEINFINITE);
				invoker.weaponstatus[BOSSS_HAND]=hnd;
				A_PlaySound("weapons/pocket",CHAN_WEAPON);
			}
		}
	loadone:
		BARG A 2 offset(16,50) A_JumpIf(invoker.weaponstatus[BOSSS_HAND]<1,"loadhandnext");
		BARG A 4 offset(14,46){
			invoker.weaponstatus[BOSSS_HAND]--;
			invoker.weaponstatus[BOSSS_MAG]++;
			A_PlaySound("weapons/rifleclick2",CHAN_WEAPON);
		}loop;
	loadhandnext:
		BARG A 8 offset(16,48){
			if(
				PressingReload()||
				PressingFire()||
				PressingAltFire()||
				PressingZoom()||
				!countinv("SevenMilAmmo")	//don't strip clips automatically
			)setweaponstate("reloaddone");
			else A_PlaySound("weapons/pocket");
		}goto loadhandloop;
	loadclip:
		BARG AA 4 offset(16,50) A_PlaySound("weapons/rifleclick2");
		BARG AA 3 offset(17,52) A_PlaySound("weapons/rifleclick2");
		BARG AAAAAA 2 offset(16,50) A_PlaySound("weapons/rifleclick2");
		BARG A 4 offset(14,46){
			A_PlaySound("weapons/rifleclick",CHAN_WEAPON);
			let ccc=hdmagammo(findinventory("HD7mClip"));
			if(ccc){
				invoker.weaponstatus[BOSSS_MAG]=ccc.TakeMag(true);
			}
		}goto reloaddone;
	reloaddone:
		BARG A 1 offset(4,40);
		BARG A 1 offset(2,36);
		BARG A 1 offset(0,34);
		goto ready;
	unload:
		BARG A 1 offset(0,34);
		BARG A 1 offset(2,36);
		BARG A 1 offset(4,40);
		BARG A 2 offset(8,42){
			A_MuzzleClimb(-frandom(0.4,0.8),frandom(0.4,1.4));
			A_PlaySound("weapons/rifleclick2",CHAN_WEAPON);
		}
		BARG A 4 offset (14,46){
			A_MuzzleClimb(-frandom(0.4,0.8),frandom(0.4,1.4));
			A_PlaySound("weapons/rifleload",CHAN_WEAPON);
		}
	unloadloop:
		BARG A 4 offset(3,41){
			if(invoker.weaponstatus[BOSSS_MAG]<1)setweaponstate("unloaddone");
			else{
				A_PlaySound("weapons/rifleclick2");
				invoker.weaponstatus[BOSSS_MAG]--;
				if(A_JumpIfInventory("SevenMilAmmo",0,"null")){
					A_SpawnItemEx(
						"HDLoose7mm",cos(pitch)*8,0,height-7-sin(pitch)*8,
						cos(pitch)*cos(angle-40)*1+vel.x,
						cos(pitch)*sin(angle-40)*1+vel.y,
						-sin(pitch)*1+vel.z,
						0,SXF_ABSOLUTEMOMENTUM|
						SXF_NOCHECKPOSITION|SXF_TRANSFERPITCH
					);
				}else A_GiveInventory("SevenMilAmmo",1);
			}
		}
		BARG A 2 offset(2,42);
		BARG A 0{
			if(
				PressingReload()||
				PressingFire()||
				PressingAltFire()||
				PressingZoom()
			)setweaponstate("unloaddone");
		}loop;
	unloaddone:
		BARG A 2 offset(2,42);
		BARG A 3 offset(3,41);
		BARG A 1 offset(4,40) A_PlaySound("weapons/rifleclick",CHAN_WEAPON);
		BARG A 1 offset(2,36);
		BARG A 1 offset(0,34);
		goto ready;

	spawn:
		BORF A -1;
	}
	override void InitializeWepStats(bool idfa){
		weaponstatus[BOSSS_CHAMBER]=2;
		weaponstatus[BOSSS_MAG]=10;
		if(!idfa){
			weaponstatus[BOSSS_HEAT]=0;
		}
		if(!owner){
			if(randompick(0,0,1))weaponstatus[0]&=~BOSSF_FRONTRETICLE;
				else weaponstatus[0]|=BOSSF_FRONTRETICLE;
			if(random(0,20))weaponstatus[0]&=~BOSSF_CUSTOMCHAMBER;
				else weaponstatus[0]|=BOSSF_CUSTOMCHAMBER;
			weaponstatus[BOSSS_ZOOM]=20;
			weaponstatus[BOSSS_DROPADJUST]=100;
		}
	}
}
enum bossstatus{
	BOSSF_FRONTRETICLE=1,
	BOSSF_CUSTOMCHAMBER=2,
	BOSSF_UNLOADONLY=4,

	BOSSS_CHAMBER=1, //0=nothing, 1=brass, 2=loaded, 3/4=jammed brass/round
	BOSSS_MAG=2,
	BOSSS_ZOOM=3,
	BOSSS_DROPADJUST=4,
	BOSSS_HEAT=5,
	BOSSS_HAND=6,
	BOSSS_GRIME=7,
}


